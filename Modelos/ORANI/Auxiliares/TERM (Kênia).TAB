! TERM: fast multi-region model designed by Mark Horridge, 2002-10 !
! Version for TERM Training Course, South Africa, Jan 2011 ; updated Jan 2012 !

! Excerpt 1 of TABLO input file: !
! Files and sets !
File  INFILE # Base data flows and elasticities (input)#;
      REGSETS # Sets file (input)#;
(new) SUMMARY # File for data summary and diagnostics (output)#;

Set                                           ! subscript !
 SRC (dom,imp);                                   !s!
 COM # Commodities # size 27
    read elements from file REGSETS header "COM"; !c!
 MAR # Margin coms #
    read elements from file REGSETS header "MAR"; !m!
 IND # Industries # size 27
    read elements from file REGSETS header "IND"; !i!
 OCC # Labour skill categories # size 11
    read elements from file REGSETS header "OCC"; !o!
 DST # Regions of use # size 9
    read elements from file REGSETS header "REGD";!d!
 ORG # Regions of origin # size 9
    read elements from file REGSETS header "REGS";!r!
 PRD # Regions of margin production # size 9
    read elements from file REGSETS header "REGP";!p!

Set REG = DST intersect ORG; ! special set for comparing DST ORG PRD !
Subset
 MAR is subset of COM;
 REG is subset of PRD;
 PRD is subset of ORG;
Set
 FINDEM # Final demanders #  (HOU, INV, GOV, EXP);!f!
 INT # Intermediate # (INT);
 USR # Users #    = IND + FINDEM;                 !u!
 MAINUSR # Main users #    = INT + FINDEM;
 NONMAR # Non-margin goods # = COM-MAR;

Zerodivide off;

! Excerpt 2 of TABLO input file: !
! Flow values from data file !

! Domestic basic prices = Output prices 
  Imported basic prices = CIF prices 
       Delivered values = Basic + Margins
     Purchasers' values = Basic + Margins + Tax = Delivered + Tax !

Coefficient
(all,c,COM)(all,i,IND)(all,d,DST)
   MAKE(c,i,d) # MAKE multiproduction matrix #;
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
   USE(c,s,u,d) # Delivered value of demands: basic + margins (ex-tax) #;
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
   TAX(c,s,u,d) # Commodity taxes #;
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
   TRADE(c,s,r,d) # Sourcing matrix (at basic prices) #;
(all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
   TRADMAR(c,s,m,r,d) # Margins on trade matrix (at basic prices)#;
(all,m,MAR)(all,r,ORG)(all,d,DST)(all,p,PRD)
   SUPPMAR(m,r,d,p) # Margins supplied by p on goods passing from r to d #;
(all,c,COM)(all,i,IND)(all,d,DST)
   INVEST(c,i,d) # Investment at purchasers prices #;
(all,i,IND)(all,d,DST)            STOCKS(i,d)  # Domestic inventories #;
(all,i,IND)(all,o,OCC)(all,d,DST) LAB(i,o,d)   # Wage matrix #;
(all,i,IND)(all,d,DST)            CAP(i,d)     # Rentals to capital #;
(all,i,IND)(all,d,DST)            LND(i,d)     # Rentals to land #;
(all,i,IND)(all,d,DST)            PRODTAX(i,d) # Taxes on production #;
Read
 MAKE    from file INFILE header "MAKE";
 USE     from file INFILE header "BSMR";
 TAX     from file INFILE header "UTAX";
 TRADE   from file INFILE header "TRAD";
 SUPPMAR from file INFILE header "MARS";
 TRADMAR from file INFILE header "TMAR";
 INVEST  from file INFILE header "2PUR";
 STOCKS  from file INFILE header "STOK";
 LAB     from file INFILE header "1LAB";
 CAP     from file INFILE header "1CAP";
 LND     from file INFILE header "1LND";
 PRODTAX from file INFILE header "1PTX";

! Excerpt 3 of TABLO input file: !
! Sign and other checks on initial flow data !
Assertion 
(initial) # USE>=0 #     (all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
   USE(c,s,u,d) >=0;
(initial) # TRADE>=0 #   (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
   TRADE(c,s,r,d)>=0;
(initial) # TRADMAR>=0 # (all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
   TRADMAR(c,s,m,r,d)>=0;
(initial) # SUPPMAR>=0 # (all,m,MAR)(all,r,ORG)(all,d,DST)(all,p,PRD)
   SUPPMAR(m,r,d,p) >=0;
(initial) # INVEST>=0 #  (all,c,COM)(all,i,IND)(all,d,DST) INVEST(c,i,d)>=0;
(initial) # MAKE>=0 #    (all,c,COM)(all,i,IND)(all,d,DST) MAKE(c,i,d)>=0;
(initial) # LAB>=0 #     (all,i,IND)(all,o,OCC)(all,d,DST) LAB(i,o,d) >=0;
(initial) # CAP>=0 #     (all,i,IND)(all,d,DST)            CAP(i,d)   >=0;
(initial) # LND>=0 #     (all,i,IND)(all,d,DST)            LND(i,d)   >=0;

! Check no taxes or margins on zero flows !
(initial) # USE=0 --> TAX=0 # (all,c,COM)(all,s,SRC)(all,u,USR)
  (all,d,DST:USE(c,s,u,d)=0) TAX(c,s,u,d)=0;
(initial) # TRADE=0 --> TRADMAR=0 # (all,c,COM)(all,s,SRC)(all,r,ORG)
   (all,d,DST:TRADE(c,s,r,d)=0)(all,m,MAR) TRADMAR(c,s,m,r,d)=0;

! Excerpt 4 of TABLO input file: !
! Elasticities !
Coefficient
(parameter)(all,i,IND) SIGMALAB(i)    # CES substitution between skill types #;
(parameter)(all,i,IND) SIGMAPRIM(i)   # CES substitution, primary factors #;
(parameter)(all,c,COM) SIGMADOMIMP(c) # Substitution elast between dom/imp #;
(parameter)(all,c,COM) SIGMADOMDOM(c) # Substitution elast between origins #;
(parameter)(all,m,MAR) SIGMAMAR(m)    # Subst elast between margin origins #;
(parameter)(all,d,DST) POP(d)         # Population #;
(parameter)(all,i,IND) SIGMAOUT(i)    # CET transformation elasticities #;
(parameter)(all,c,COM) EXP_ELAST(c)   # Export demand elasticities #;
Read
 SIGMALAB    from file INFILE header "SLAB";
 SIGMAPRIM   from file INFILE header "P028";
 SIGMADOMIMP from file INFILE header "P015";
 SIGMADOMDOM from file INFILE header "SGDD";
 SIGMAMAR    from file INFILE header "SMAR";
 POP         from file INFILE header "PO01";
 SIGMAOUT    from file INFILE header "SCET";
 EXP_ELAST   from file INFILE header "P018";
! Check elasticities are right sign !
Assertion 
(initial) # SIGMALAB>=0 #    (all,i,IND) SIGMALAB(i)    >=0;
(initial) # SIGMAPRIM>=0 #   (all,i,IND) SIGMAPRIM(i)   >=0;
(initial) # SIGMADOMIMP>=0 # (all,c,COM) SIGMADOMIMP(c) >=0;
(initial) # SIGMADOMDOM>=0 # (all,c,COM) SIGMADOMDOM(c) >=0;
(initial) # SIGMAMAR>=0 #    (all,m,MAR) SIGMAMAR(m)    >=0;
(initial) # POP>=0 #         (all,d,DST) POP(d)         >=0;
(initial) # SIGMAOUT>=0 #    (all,i,IND) SIGMAOUT(i)    >=0;
(initial) # EXP_ELAST>=0 #   (all,c,COM) EXP_ELAST(c)   >=0;

! Excerpt 5 of TABLO input file: !
! Variables used to update flows !
Variable
(all,c,COM)(all,i,IND)(all,d,REG)
   xmake(c,i,d) # Output of good c by industry i in d #;
(all,c,COM)(all,i,IND)(all,d,REG)
   pmake(c,i,d) # Price received by industries #;
(all,c,COM)(all,r,ORG)
   pdom(c,r) # Output prices = basic prices of domestic goods #;
(all,c,COM)(all,s,SRC)(all,r,ORG)
   pbasic(c,s,r) # Basic prices #;
(all,c,COM)(all,s,SRC)(all,d,DST)
   puse(c,s,d) # Delivered price of regional composite good c,s to d #;
(all,m,MAR)(all,r,ORG)(all,d,DST)(all,p,PRD)
   xsuppmar(m,r,d,p) # Demand for margin m (made in p) on goods from r to d #;
(all,m,MAR)(all,r,ORG)(all,d,DST)
   psuppmar_p(m,r,d) # Price of composite margin m on goods from r to d #;
(all,m,MAR)(all,r,ORG)(all,d,DST)
   xsuppmar_p(m,r,d) # Quantity of composite margin m on goods from r to d #;
(all,m,MAR)(all,p,PRD)
   xsuppmar_rd(m,p) # Total demand for margins produced in p #;
(all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
   xtradmar(c,s,m,r,d) # Margin m on good c,s going from r to d #;
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
   xtrad(c,s,r,d) # Quantity of good c,s from r to d #;
(all,c,COM)(all,d,DST)
   pinvest(c,d) # Purchaser's price of good c for investment in d #;
(all,c,COM)(all,i,IND)(all,d,DST)
   xinvi(c,i,d) # Amount of good c for investment, industry i in d #;
(all,c,COM)(all,s,SRC)(all,i,IND)(all,d,DST)
   xint(c,s,i,d) # Intermediate demands for all-region composite #;
(all,c,COM)(all,s,SRC)(all,d,DST)
   xhou(c,s,d) # Household demands for all-region composite #;
(all,c,COM)(all,s,SRC)(all,d,DST)
   xgov(c,s,d) # Government demands for all-region composite #;
(all,c,COM)(all,s,SRC)(all,d,DST)
   xinv(c,s,d) # Investment demands for all-region composite #;
(all,c,COM)(all,s,SRC)(all,d,DST)
   xexp(c,s,d) # Export of all-region composite leaving port at d #;

(all,i,IND)(all,o,OCC)(all,d,DST) xlab(i,o,d) # Labour demands #;
(all,i,IND)(all,o,OCC)(all,d,DST) plab(i,o,d) # Wage rates #;
(all,i,IND)(all,d,DST) xtot(i,d) # Industry outputs #;
(all,i,IND)(all,d,DST) ptot(i,d) # Industry output prices #;
(all,i,IND)(all,d,DST) xcap(i,d) # Capital usage #;
(all,i,IND)(all,d,DST) xlnd(i,d) # Land usage #;
(all,i,IND)(all,d,DST) pcap(i,d) # Rental price of capital #;
(all,i,IND)(all,d,DST) plnd(i,d) # Rental price of land #;

(all,i,IND)(all,d,DST) xstocks(i,d) # Inventories #;

(change)(all,i,IND)(all,d,DST)
 delPTX(i,d) # Ordinary change in production tax revenue #;
(change)(all,c,COM)(all,s,SRC)(all,d,DST)(all,i,IND)
 delTAXint(c,s,i,d)# Ordinary change in intermediate commodity tax revenue #;
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
 delTAXhou(c,s,d)# Ordinary change in household commodity tax revenue #;
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
 delTAXinv(c,s,d)# Ordinary change in investment commodity tax revenue #;
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
 delTAXgov(c,s,d)# Ordinary change in government commodity tax revenue #;
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
 delTAXexp(c,s,d)# Ordinary change in export commodity tax revenue #;

! Excerpt 6 of TABLO input file: !
! Update of flows !
Update
(all,c,COM)(all,i,IND)(all,d,REG)
   MAKE(c,i,d) = xmake(c,i,d)*pmake(c,i,d);
(all,c,COM)(all,s,SRC)(all,i,IND)(all,d,DST)
   USE(c,s,i,d) = xint(c,s,i,d)*puse(c,s,d);
(all,c,COM)(all,s,SRC)(all,d,DST)
   USE(c,s,"hou",d) = xhou(c,s,d)*puse(c,s,d);
(all,c,COM)(all,s,SRC)(all,d,DST)
   USE(c,s,"inv",d) = xinv(c,s,d)*puse(c,s,d);
(all,c,COM)(all,s,SRC)(all,d,DST)
   USE(c,s,"gov",d) = xgov(c,s,d)*puse(c,s,d);
(all,c,COM)(all,s,SRC)(all,d,DST)
   USE(c,s,"exp",d) = xexp(c,s,d)*puse(c,s,d);
(change)(all,c,COM)(all,s,SRC)(all,i,IND)(all,d,DST)
   TAX(c,s,i,d) = delTAXint(c,s,i,d);
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
   TAX(c,s,"hou",d) = delTAXhou(c,s,d);
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
   TAX(c,s,"inv",d) = delTAXinv(c,s,d);
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
   TAX(c,s,"gov",d) = delTAXgov(c,s,d);
(change)(all,c,COM)(all,s,SRC)(all,d,DST)
   TAX(c,s,"exp",d) = delTAXexp(c,s,d);
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
   TRADE(c,s,r,d) = pbasic(c,s,r)*xtrad(c,s,r,d);
(all,m,MAR)(all,r,ORG)(all,d,DST)(all,p,PRD)
   SUPPMAR(m,r,d,p) = xsuppmar(m,r,d,p)*pdom(m,p);
(all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
   TRADMAR(c,s,m,r,d) =  xtradmar(c,s,m,r,d)*psuppmar_p(m,r,d);
(all,c,COM)(all,i,IND)(all,d,DST)
   INVEST(c,i,d) = pinvest(c,d)*xinvi(c,i,d);
(all,i,IND)(all,o,OCC)(all,d,DST) LAB(i,o,d) = plab(i,o,d)*xlab(i,o,d);
(all,i,IND)(all,d,DST) CAP(i,d) = pcap(i,d)*xcap(i,d);
(all,i,IND)(all,d,DST) LND(i,d) = plnd(i,d)*xlnd(i,d);
(all,i,IND)(all,d,DST) STOCKS(i,d) = ptot(i,d)*xstocks(i,d);
(change)(all,i,IND)(all,d,DST) PRODTAX(i,d) = delPTX(i,d);

! Excerpt 7 of TABLO input file: !
! Basic prices: pbasic is alias of pdom and pimp !
Variable                phi        # Exchange rate, local currency/$world #;
 (all,c,COM)(all,r,ORG) pimp(c,r)  # Import prices, local currency #;
 (all,c,COM)(all,r,ORG) pfimp(c,r) # Import prices, foreign currency #;
Equation E_pimp (all,c,COM)(all,r,ORG) pimp(c,r)         = pfimp(c,r) + phi;
      E_pbasicA (all,c,COM)(all,r,ORG) pbasic(c,"dom",r) = pdom(c,r);
      E_pbasicB (all,c,COM)(all,r,ORG) pbasic(c,"imp",r) = pimp(c,r);

! Excerpt 8 of TABLO input file: !
! Purchasers prices, tax rates and imp/dom shares !
Variable
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
   ppur(c,s,u,d) # User (purchasers) prices, inc margins and taxes #;
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
   tuser(c,s,u,d) # Powers of commodity taxes #;
(all,c,COM)(all,s,SRC) tuser_ud(c,s) # Tax shifter by commodity and source #;
(all,c,COM)(all,d,DST) tuser_su(c,d) # Tax shifter by commodity and dest #;
 
! tax-inclusive user prices of delivered composites !
Equation E_ppur # Purchasers prices #
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
 ppur(c,s,u,d) = puse(c,s,d) + tuser(c,s,u,d);

Equation E_tuser # Tax rate driver equation #
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
     tuser(c,s,u,d) = tuser_ud(c,s) + tuser_su(c,d);
Backsolve tuser using E_tuser;

Coefficient
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST) PUR(c,s,u,d) # Purchasers values#;
(all,c,COM)(all,s,SRC)(all,u,USR) PUR_D(c,s,u) # Purchasers values, national #;
(all,c,COM)(all,u,USR)(all,d,DST) PUR_S(c,u,d)   # Purch. values, sum over s #;
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST) SRCSHR(c,s,u,d) # Imp/dom shares#;
(all,u,USR)(all,d,DST) PUR_CS(u,d) # Expenditure on goods #;
Zerodivide default 0.5;
Formula
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
            PUR(c,s,u,d) = USE(c,s,u,d) + TAX(c,s,u,d);
(all,c,COM)(all,s,SRC)(all,u,USR)
            PUR_D(c,s,u) = sum{d,DST,PUR(c,s,u,d)};
(all,c,COM)(all,u,USR)(all,d,DST)
            PUR_S(c,u,d) = sum{s,SRC,PUR(c,s,u,d)};
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
            SRCSHR(c,s,u,d) = PUR(c,s,u,d)/PUR_S(c,u,d);
(all,u,USR)(all,d,DST)
            PUR_CS(u,d)=sum{c,COM,PUR_S(c,u,d)};
Zerodivide off;

Coefficient
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST) TAXRATE(c,s,u,d) # Tax rate #;
Zerodivide default 0.0;
Formula
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
            TAXRATE(c,s,u,d) = TAX(c,s,u,d)/USE(c,s,u,d);
Zerodivide off;
Write TAXRATE to file SUMMARY header "TRAT";

Variable
(all,c,COM)(all,u,USR)(all,d,DST) ppur_s(c,u,d) # User prices, average over s#;
 (all,c,COM)(all,d,DST) phou(c,d) # Household price of composites #;
Equation E_ppur_s
(all,c,COM)(all,u,USR)(all,d,DST)
          ppur_s(c,u,d) = sum{s,SRC,SRCSHR(c,s,u,d)*ppur(c,s,u,d)};
 E_phou # Alias #
  (all,c,COM)(all,d,DST) phou(c,d) = ppur_s(c,"hou",d);

! Excerpt 9 of TABLO input file: !
! Dom/imp substitution by industries, households and capital creators !
Variable
 (all,c,COM)(all,i,IND)(all,d,DST)
                     xint_s(c,i,d) # Industry demands for dom/imp composite #;
(all,c,COM)(all,d,DST) xhou_s(c,d) # Household demands for dom/imp composite #;
(all,c,COM)(all,d,DST) xinv_s(c,d) # Investment demands for dom/imp composite #;
Equation
 E_xint (all,c,COM)(all,s,SRC)(all,i,IND)(all,d,DST)
xint(c,s,i,d) = xint_s(c,i,d) - SIGMADOMIMP(c)*[ppur(c,s,i,d)-ppur_s(c,i,d)];
 E_xhou (all,c,COM)(all,s,SRC)(all,d,DST)
xhou(c,s,d) = xhou_s(c,d) - SIGMADOMIMP(c)*[ppur(c,s,"hou",d)-phou(c,d)];
 E_xinv (all,c,COM)(all,s,SRC)(all,d,DST)
xinv(c,s,d) = xinv_s(c,d) - SIGMADOMIMP(c)*[ppur(c,s,"inv",d)-pinvest(c,d)];

! Excerpt 10 of TABLO input file: !
! Intermediate demands !

Variable
 (all,i,IND)(all,d,DST) atot(i,d) # All-input-augmenting technical change #;
 (all,c,COM)(all,i,IND)(all,d,DST) aint_s(c,i,d) # Intermediate tech change #;
 (all,i,IND)  bint_scd(i) # Driver: intermediate tech change #;
 (all,i,IND)(all,d,DST) pint(i,d) # Intermediate effective price indices #;
 (all,c,COM)(all,i,IND)(all,d,DST) bint_s(c,i,d) # Intermediate tech change #;
Equation
 E_aint_s (all,c,COM)(all,i,IND)(all,d,DST)
   aint_s(c,i,d) = bint_scd(i) + bint_s(c,i,d);
 E_xint_s (all,c,COM)(all,i,IND)(all,d,DST)
   xint_s(c,i,d) = atot(i,d) + aint_s(c,i,d) + xtot(i,d)
             -0.15*{ppur_s(c,i,d) + aint_s(c,i,d) - pint(i,d)};

 E_pint   (all,i,IND)(all,d,DST)
   ID01(PUR_CS(i,d))*pint(i,d)
  = sum{c,COM,PUR_S(c,i,d)*[ppur_s(c,i,d)+aint_s(c,i,d)]};
 ! Sep 06: addition of some intermediate substitution. Works faster if pint
is NOT substituted !

! Excerpt 11 of TABLO input file: !
! Occupational composition of labour demand !

Coefficient
 (all,i,IND)(all,d,DST) LAB_O(i,d)   # Total labour bill in industry i #;
Formula
 (all,i,IND)(all,d,DST) LAB_O(i,d) = sum{o,OCC, LAB(i,o,d)};
Variable
 (all,i,IND)(all,d,DST) plab_o(i,d) # Price of labour composite #;
 (all,i,IND)(all,d,DST) xlab_o(i,d) # Effective labour input #;
 (all,i,IND)(all,d,DST) wlab_o(i,d) # Wage bills #;
Equation
 E_xlab   # Demand for labour by industry and skill group #
  (all,i,IND)(all,o,OCC)(all,d,DST)
   xlab(i,o,d) = xlab_o(i,d) - SIGMALAB(i)*[plab(i,o,d) - plab_o(i,d)];
 E_plab_o # Price to each industry of labour composite #
  (all,i,IND)(all,d,DST)
 ID01(LAB_O(i,d))*plab_o(i,d) = sum{o,OCC, LAB(i,o,d)*plab(i,o,d)};
 E_wlab_o # Wage bills #
  (all,i,IND)(all,d,DST)
 ID01(LAB_O(i,d))*wlab_o(i,d) = sum{o,OCC,LAB(i,o,d)*[plab(i,o,d)+xlab(i,o,d)]};

! Excerpt 12 of TABLO input file: !
! Factor demands !

Coefficient
 (all,i,IND)(all,d,DST) PRIM(i,d) # Total factor input to industry i#;
Formula
 (all,i,IND)(all,d,DST) PRIM(i,d) = LAB_O(i,d)+ CAP(i,d) + LND(i,d);
Variable
 (all,i,IND)(all,d,DST)
 pprim(i,d)  # Effective price of primary factor composite #;
 (all,i,IND)(all,d,DST) xprim(i,d)  # Primary factor composite #;
 (all,i,IND)(all,d,DST) alab_o(i,d) # Labor-augmenting technical change #;
 (all,i,IND)(all,d,DST) acap(i,d)   # Capital-augmenting technical change #;
 (all,i,IND)(all,d,DST) alnd(i,d)   # Land-augmenting technical change #;
 (all,i,IND)(all,d,DST) aprim(i,d)  # Primary-factor-augmenting tech change #;
 (all,i,IND)(all,d,DST) wprim(i,d)  # Primary factor payments #;
(change)(all,i,IND)(all,d,DST)
 delPRIM(i,d)# Ordinary change in cost of primary factors #;

Equation
 E_xlab_o  # Industry demands for effective labour #
  (all,i,IND)(all,d,DST) xlab_o(i,d) - alab_o(i,d) =
   xprim(i,d) - SIGMAPRIM(i)*[plab_o(i,d) + alab_o(i,d) - pprim(i,d)];

 E_pcap  # Industry demands for capital #
  (all,i,IND)(all,d,DST)  xcap(i,d) - acap(i,d) =
   xprim(i,d) - SIGMAPRIM(i)*[pcap(i,d) + acap(i,d) - pprim(i,d)];

 E_plnd  # Industry demands for land #
  (all,i,IND)(all,d,DST)  xlnd(i,d) - alnd(i,d) =
   xprim(i,d) - SIGMAPRIM(i)*[plnd(i,d) + alnd(i,d) - pprim(i,d)];

 E_pprim  # Effective price term for factor demand equations #
  (all,i,IND)(all,d,DST)
ID01(PRIM(i,d))*pprim(i,d) =
 LAB_O(i,d)*[plab_o(i,d) + alab_o(i,d)]
  + CAP(i,d)*[pcap(i,d) + acap(i,d)] + LND(i,d)*[plnd(i,d) + alnd(i,d)];

 E_xprim  # Use of composite primary factor #
  (all,i,IND)(all,d,DST) xprim(i,d) = xtot(i,d)+atot(i,d)+aprim(i,d);

E_wprim  # Primary factor payments #
  (all,i,IND)(all,d,DST)
ID01[PRIM(i,d)]*wprim(i,d) = CAP(i,d) *[pcap(i,d) + xcap(i,d)]
                              +  LND(i,d) *[plnd(i,d) + xlnd(i,d)]
                    + sum{o,OCC, LAB(i,o,d)* [plab(i,o,d) + xlab(i,o,d)]};

 E_delPRIM  # Ordinary change in total cost of primary factors #
  (all,i,IND)(all,d,DST) ! currently unused !
100*delPRIM(i,d) = PRIM(i,d)*wprim(i,d);

Set FAC(LND,LAB,CAP);
Coefficient ! table to see primary factor shares, etc !
 (all,i,IND)(all,f,FAC)(all,d,DST) PRIMCOST(i,f,d) # Total factor inputs #;
Formula
 (all,i,IND)(all,d,DST) PRIMCOST(i,"Lnd",d) = LND(i,d);
 (all,i,IND)(all,d,DST) PRIMCOST(i,"Lab",d) = LAB_O(i,d);
 (all,i,IND)(all,d,DST) PRIMCOST(i,"Cap",d) = CAP(i,d);
 Write PRIMCOST to file SUMMARY header "PCST";

! Excerpt 13 of TABLO input file: !
! Output prices !

Coefficient  ! for reporting or analysis only !
 (all,i,IND)(all,d,DST)  VARCST(i,d)  # Shortrun variable cost of industry i #;
Formula
 (all,i,IND)(all,d,DST)  VARCST(i,d)  = LAB_O(i,d)  + PUR_CS(i,d);
Variable (all,i,IND)(all,d,DST)
   pvar(i,d)   # Shortrun variable cost of production #;
Equation
E_pvar (all,i,IND)(all,d,DST)  ID01(VARCST(i,d))*[pvar(i,d)-atot(i,d)] =
 LAB_O(i,d)*[plab_o(i,d) + alab_o(i,d)] +  PUR_CS(i,d)*pint(i,d);

Coefficient
 (all,i,IND)(all,d,DST)  VCST(i,d)    # Total cost of industry i #;
Formula
 (all,i,IND)(all,d,DST)  VCST(i,d)  = PRIM(i,d)  + PUR_CS(i,d);
Variable (all,i,IND)(all,d,DST)
   pcst(i,d)   # Ex-tax cost of production #;

Equation
E_pcst (all,i,IND)(all,d,DST)  ID01(VCST(i,d))*[pcst(i,d)-atot(i,d)] =
 PRIM(i,d)*[aprim(i,d)+pprim(i,d)] +  PUR_CS(i,d)*pint(i,d);

Coefficient
 (all,i,IND)(all,d,DST)  VTOT(i,d)    # Total industry cost plus tax #;
 (all,i,IND)(all,d,DST) PTXRATE(i,d)   # Rate of production tax #;
Zerodivide default 1.0;
Formula
 (all,i,IND)(all,d,DST)  VTOT(i,d)    = VCST(i,d) + PRODTAX(i,d);
 (all,i,IND)(all,d,DST)  PTXRATE(i,d)  = PRODTAX(i,d)/VCST(i,d);
Zerodivide off;
Write PTXRATE to file SUMMARY header "PTXR";
Variable
 (change)(all,i,IND)(all,d,DST)
   delPTXRATE(i,d) # Change in rate of production tax #;

Equation
 E_delPTX  (all,i,IND) (all,d,DST) delPTX(i,d) =
  0.01*PRODTAX(i,d)*[xtot(i,d)+pcst(i,d)]    +    VCST(i,d)*delPTXRATE(i,d);
Equation
 E_ptot     (all,i,IND)(all,d,DST)
 ID01(VTOT(i,d))*[ptot(i,d) + xtot(i,d)] =
 VCST(i,d)*[pcst(i,d) + xtot(i,d)] + 100*delPTX(i,d);

Set COSTCAT # Cost Categories #
 (INTDOM, INTIMP, COMTAX, LAB, CAP, LND, PRODTAX);
Coefficient
 (all,i,IND)(all,co,COSTCAT)(all,d,DST) COSTMAT(i,co,d) # Cost Matrix #;
Formula
 (all,i,IND)(all,d,DST) COSTMAT(i,"IntDom",d)  = sum{c,COM, USE(c,"dom",i,d)};
 (all,i,IND)(all,d,DST) COSTMAT(i,"IntImp",d)  = sum{c,COM, USE(c,"imp",i,d)};
 (all,i,IND)(all,d,DST)
                COSTMAT(i,"ComTax",d)  = sum{c,COM, sum{s,SRC, TAX(c,s,i,d)}};
 (all,i,IND)(all,d,DST) COSTMAT(i,"Lab",d)     = LAB_O(i,d);
 (all,i,IND)(all,d,DST) COSTMAT(i,"Cap",d)     = CAP(i,d);
 (all,i,IND)(all,d,DST) COSTMAT(i,"Lnd",d)     = LND(i,d);
 (all,i,IND)(all,d,DST) COSTMAT(i,"ProdTax",d) = PRODTAX(i,d);
Write COSTMAT to file SUMMARY header "CSTM";
Write VTOT    to file SUMMARY header "VTOT";

! Excerpt 14 of TABLO input file: !
! Household demands for imp/dom composite commodities !
! follows LES/Stone-Geary/Klein-Rubin scheme !

! Multiple households version !
Set HOU # Households # read elements from file INFILE header "HOU";
Coefficient (all,c,COM)(all,h,HOU)(all,d,DST) HOUPUR(c,d,h)
 # Household demands at purchasers prices #;
Read HOUPUR from file INFILE header "3PUR";
Coefficient (all,h,HOU)(all,d,DST) HOUPUR_C(h,d) # Household totals #;
Formula     (all,h,HOU)(all,d,DST) HOUPUR_C(h,d) = sum{c,COM, HOUPUR(c,d,h)};

Variable
 (all,c,COM)(all,h,HOU)(all,d,DST) xhouh_s(c,h,d) # Household demands #;
 (all,h,HOU)(all,d,DST)
   wlux(h,d) # Total nominal supernumerary household expenditure #;
 (all,h,HOU)(all,d,DST) xhouhtot(h,d) # Total real household consumption #;
 (all,h,HOU)(all,d,DST) whouhtot(h,d) # Total nominal household consumption #;
 (all,h,HOU)(all,d,DST) phouhtot(h,d) # CPI #;
 (all,h,HOU)(all,d,DST) nhouh(h,d)    # Number of households #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   xlux(c,h,d) # Household - supernumerary demands #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   xsub(c,h,d) # Household - subsistence demands #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   alux(c,h,d) # Taste change, supernumerary demands #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   asub(c,h,d) # Taste change, subsistence demands #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   ahou_s(c,h,d) # Taste change,household imp/dom composite#;
Coefficient
 (all,c,COM)(all,h,HOU)(all,d,DST)
   SLUX(c,h,d) # Marginal household budget shares #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   EPSH(c,d,h)  # Household expenditure elasticities #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   BUDGSHR(c,h,d) # Household average budget shares #;
 (all,h,HOU)(all,d,DST)            EPSAVE(h,d) # Average EPS: should be 1 #;
 (all,h,HOU)(all,d,DST)
   FRISCHH(d,h) # Frisch LES 'parameter'= - (total/luxury) #;
 (all,c,COM)(all,h,HOU)(all,d,DST)
   BLUX(c,h,d) # Ratio, (supernumerary expenditure/total expenditure) #;
Read   FRISCHH from file INFILE header "P21H";
          EPSH from file INFILE header "XPLH";
Formula
 (all,c,COM)(all,h,HOU)(all,d,DST) BUDGSHR(c,h,d) = HOUPUR(c,d,h)/HOUPUR_C(h,d);
 (all,h,HOU)(all,d,DST) EPSAVE(h,d) = sum{c,COM, EPSH(c,d,h)*BUDGSHR(c,h,d)};
! scale EPS so they average to 1: else they tend to drift off in recursive sims!
(initial) (all,c,COM)(all,h,HOU)(all,d,DST) EPSH(c,d,h)=EPSH(c,d,h)/EPSAVE(h,d);
 (all,c,COM)(all,h,HOU)(all,d,DST)  BLUX(c,h,d) = ABS[EPSH(c,d,h)/FRISCHH(d,h)];
 (all,c,COM)(all,h,HOU)(all,d,DST)  SLUX(c,h,d) = EPSH(c,d,h)*BUDGSHR(c,h,d);
Write  SLUX    to file SUMMARY header "LSHR";
       BUDGSHR to file SUMMARY header "BSHR";
       EPSAVE  to file SUMMARY header "AVEP";
Update
(change)(all,h,HOU)(all,d,DST)
FRISCHH(d,h) = FRISCHH(d,h)*[whouhtot(h,d)-wlux(h,d)]/100.0;
(change)(all,c,COM)(all,h,HOU)(all,d,DST)
 EPSH(c,d,h) = EPSH(c,d,h)
  *[xlux(c,h,d)- xhouh_s(c,h,d)+whouhtot(h,d)-wlux(h,d)]/100.0;
(all,c,COM)(all,h,HOU)(all,d,DST) HOUPUR(c,d,h) = phou(c,d)*xhouh_s(c,h,d);

Coefficient
 (all,c,COM)(all,d,DST) EPS(c,d)  # Dummy expenditure elasticities #;
Formula (initial) (all,c,COM)(all,d,DST) EPS(c,d) =1;
Read  (IfHeaderExists) EPS from file Infile header "XPEL";
Update (change)(all,c,COM)(all,d,DST) EPS(c,d) =0;

Equation
 E_xsub # Subsistence demand for composite commodities #
  (all,c,COM)(all,h,HOU)(all,d,DST) xsub(c,h,d) = nhouh(h,d) + asub(c,h,d);

 E_xlux # Luxury demand for composite commodities #
  (all,c,COM)(all,h,HOU)(all,d,DST)
   xlux(c,h,d) + phou(c,d) = wlux(h,d) + alux(c,h,d);

 E_xhouh_s  # Total household demand for composite commodities #
  (all,c,COM)(all,h,HOU)(all,d,DST)
   xhouh_s(c,h,d) = BLUX(c,h,d)*xlux(c,h,d) + [1-BLUX(c,h,d)]*xsub(c,h,d);

 E_alux # Default setting for luxury taste shifter #
  (all,c,COM)(all,h,HOU)(all,d,DST)
   alux(c,h,d) = asub(c,h,d) - sum{k,COM, SLUX(k,h,d)*asub(k,h,d)};

 E_asub # Default setting for subsistence taste shifter #
  (all,c,COM)(all,h,HOU)(all,d,DST)
   asub(c,h,d) = ahou_s(c,h,d) - sum{k,COM, BUDGSHR(k,h,d)*ahou_s(k,h,d)};

 E_wlux (all,h,HOU)(all,d,DST)
   xhouhtot(h,d) = sum{c,COM,BUDGSHR(c,h,d)*xhouh_s(c,h,d)};

 E_phouhtot (all,h,HOU)(all,d,DST)
   phouhtot(h,d) = sum{c,COM,BUDGSHR(c,h,d)*phou(c,d)};

 E_whouhtot (all,h,HOU)(all,d,DST) whouhtot(h,d)= phouhtot(h,d) + xhouhtot(h,d);

Variable (change) (all,c,COM)(all,h,HOU)(all,d,DST) contCPI(c,h,d)
 # Contributions by commodity to % regional CPI #;
Coefficient (all,h,HOU)(all,d,DST) RATIOCPI(h,d)# (Current/initial) CPI#;
Formula  (initial)(all,h,HOU)(all,d,DST) RATIOCPI(h,d) = 1;
Update            (all,h,HOU)(all,d,DST) RATIOCPI(h,d) = phouhtot(h,d);
Equation E_contCPI (all,c,COM)(all,h,HOU)(all,d,DST)
  contCPI(c,h,d)= RATIOCPI(h,d)*BUDGSHR(c,h,d)*phou(c,d);

Coefficient DEFHOUSHR # Default household share #;
Formula     DEFHOUSHR = 1/sum{h,HOU,1};
Zerodivide default DEFHOUSHR;
Coefficient (all,c,COM)(all,d,DST) HOUPUR_H(c,d) # Household demand totals #;
Formula (all,c,COM)(all,d,DST) HOUPUR_H(c,d) = sum{h,HOU, HOUPUR(c,d,h)};
Coefficient (all,c,COM)(all,h,HOU)(all,d,DST) HOUSHR(c,h,d) #Household shares#;
Formula (all,c,COM)(all,h,HOU)(all,d,DST)
           HOUSHR(c,h,d) = HOUPUR(c,d,h)/HOUPUR_H(c,d);
Zerodivide off;

! we require that HOUPUR_H(c,d) = PUR_S(c,"Hou",d) !
Coefficient (all,c,COM)(all,d,DST) CHKHOUPUR(c,d) # Household demand check #;
Formula   (all,c,COM)(all,d,DST)
   CHKHOUPUR(c,d) = HOUPUR_H(c,d) - PUR_S(c,"Hou",d);
Write CHKHOUPUR to file SUMMARY header "CHOU";
!Formula   (all,c,COM)(all,d,DST)
   CHKHOUPUR(c,d) = 200*CHKHOUPUR(c,d)/ID01[HOUPUR_H(c,d) + PUR_S(c,"Hou",d)];
Write CHKHOUPUR to file SUMMARY header "CHO2" longname "HOUPUR error as %";
!
Assertion # Check HOUPUR_H = PUR_S("Hou") #  (all,c,COM)(all,d,DST)
   ABS[CHKHOUPUR(c,d)]<0.1;

Variable
 (all,d,DST) xhoutot(d) # All-household real consumption #;
 (all,d,DST) phoutot(d) # All-household CPI #;
Equation
 E_xhou_s  # All-household demand for composite commodities #
  (all,c,COM)(all,d,DST) xhou_s(c,d)= sum{h,HOU, HOUSHR(c,h,d)*xhouh_s(c,h,d)};
 E_xhoutot  # All-household demand #
(all,d,DST) sum{h,HOU, HOUPUR_C(h,d)*[xhoutot(d)-xhouhtot(h,d)]} = 0;
 E_phoutot  # All-household CPI #
(all,d,DST) sum{h,HOU, HOUPUR_C(h,d)*[phoutot(d)-phouhtot(h,d)]} = 0;

Variable (all,d,DST) nhou(d) # Number of households #;
Equation E_nhouh # Number of households #
 (all,h,HOU)(all,d,DST) nhouh(h,d) = nhou(d);

! national addup of HOU-length variables !
Variable
 (all,h,HOU) natxhouhtot(h) # National real consumption by household #;
 (all,h,HOU) natphouhtot(h) # National CPI by household #;
Equation
E_natxhouhtot # National real consumption by household #
(all,h,HOU) sum{d,DST, HOUPUR_C(h,d)*[natxhouhtot(h)-xhouhtot(h,d)]} = 0;
E_natphouhtot # National CPI by household #
(all,h,HOU) sum{d,DST, HOUPUR_C(h,d)*[natphouhtot(h)-phouhtot(h,d)]} = 0;

! Excerpt 15 of TABLO input file: !
! Investment demands and indices: conditional on industry-specific investment !
Coefficient
(all,c,COM)(all,d,DST) INVEST_I(c,d) # Investment by commodity and region #;
(all,i,IND)(all,d,DST) INVEST_C(i,d) # Investment by industry and region #;
Formula
(all,c,COM)(all,d,DST) INVEST_I(c,d) = sum{i,IND,INVEST(c,i,d)};
(all,i,IND)(all,d,DST) INVEST_C(i,d) = sum{c,COM,INVEST(c,i,d)};
Variable
(all,i,IND)(all,d,DST) xinvitot(i,d) # Investment by industry #;
(all,i,IND)(all,d,DST) pinvitot(i,d) # Investment price index by industry #;
Equation
 E_xinvi # Leontief technology for new capital creation #
  (all,c,COM)(all,i,IND)(all,d,DST) xinvi(c,i,d) = xinvitot(i,d);
 E_pinvest # Alias # (all,c,COM)(all,d,DST) pinvest(c,d) = ppur_s(c,"Inv",d);
 E_pinvitot (all,i,IND)(all,d,DST) ID01(INVEST_C(i,d))*pinvitot(i,d)
                = sum{c,COM,INVEST(c,i,d)*pinvest(c,d)};
 E_xinv_s # Add up industry demands for investment goods #
  (all,c,COM)(all,d,DST)
ID01(INVEST_I(c,d))*xinv_s(c,d)= sum{i,IND,INVEST(c,i,d)*xinvi(c,i,d)};

! Excerpt 16 of TABLO input file: !
! Industry-specific investment demands !
Variable
 (all,i,IND)(all,d,DST) gret(i,d)
   # Gross rate of return = Rental/[Price of new capital] #;
 (all,i,IND)(all,d,DST) ggro(i,d)
   # Gross growth rate of capital = Investment/Capital ratio #;
 (all,i,IND)(all,d,DST) finv1(i,d)  # Investment shift variable #;
 invslack # Investment slack variable for exogenizing national investment #;

Equation
 E_gret (all,i,IND)(all,d,DST) gret(i,d)= pcap(i,d) - pinvitot(i,d);
 E_xinvitot (all,i,IND)(all,d,DST) ggro(i,d)= xinvitot(i,d) - xcap(i,d);
 E_ggro # DPSV investment rule #
 (all,i,IND)(all,d,DST) ggro(i,d) = finv1(i,d) + 0.33*[2.0*gret(i,d) -invslack];

! Excerpt 17 of TABLO input file: !
! Government, export, and inventory demands !

Variable
(all,c,COM)(all,s,SRC)(all,d,DST) fgov(c,s,d) # Government demand shifter #;
(all,c,COM)(all,d,DST)            fgov_s(c,d) # Government demand shifter #;
(all,d,DST)                        fgovtot(d) # Government demand shifter #;
                                   fgovgen   # Economy-wide govt demand shift#;

(all,c,COM)(all,d,DST)            xgov_s(c,d) # Government demands, dom+imp #;
Equation E_xgov (all,c,COM)(all,s,SRC)(all,d,DST)
       xgov(c,s,d) =  fgovtot(d) + fgov(c,s,d) + fgov_s(c,d) + fgovgen;
Equation E_xgov_s (all,c,COM)(all,d,DST)
       xgov_s(c,d) = sum{s,SRC, SRCSHR(c,s,"Gov",d)*xgov(c,s,d)};

! exports !
Variable
(all,c,COM)(all,s,SRC) fqexp(c,s)  # Export quantity shift variable #;
(all,c,COM)(all,s,SRC) fpexp(c,s)  # Export price shift variable #;
(all,c,COM)(all,d,DST) xexp_s(c,d) # Export demands, dom+imp #;

Equation E_xexp (all,c,COM)(all,s,SRC)(all,d,DST)
   xexp(c,s,d) = fqexp(c,s) - ABS[EXP_ELAST(c)]
               * [ppur(c,s,"Exp",d) -fpexp(c,s) -phi];
Equation E_xexp_s (all,c,COM)(all,d,DST)
   xexp_s(c,d) = sum{s,SRC, SRCSHR(c,s,"exp",d)*xexp(c,s,d)};

! inventories !
Variable (all,i,IND)(all,d,DST) fstocks(i,d) # ratio, inventories/output #; 
Equation E_xstocks (all,i,IND)(all,d,DST) xstocks(i,d) = xtot(i,d)+fstocks(i,d);

! Excerpt 18 of TABLO input file: !
! Add up user demands to get total regional demand for delivered goods !
Variable
(all,c,COM)(all,s,SRC)(all,d,DST)
   xtrad_r(c,s,d) # Total demand for regional composite c,s in d #;
(all,c,COM)(all,s,SRC)(all,d,DST)
   xint_i(c,s,d) # Total intermediate demand for regional composite c,s in d #;
Coefficient (all,c,COM)(all,s,SRC)(all,d,DST)
   USE_U(c,s,d) # Total delivered value of regional composite c,s in d #;
Formula     (all,c,COM)(all,s,SRC)(all,d,DST)
   USE_U(c,s,d) = sum{u,USR, USE(c,s,u,d)};

Coefficient (all,c,COM)(all,s,SRC)(all,d,DST)
   USE_I(c,s,d) # All-intermediate delivered value of reg. composite c,s in d #;
Formula     (all,c,COM)(all,s,SRC)(all,d,DST)
   USE_I(c,s,d) = sum{i,IND, USE(c,s,i,d)};
Equation E_xint_i # Total intermediate demand for regional composite c,s in d #
(all,c,COM)(all,s,SRC)(all,d,DST)
  ID01[USE_I(c,s,d)]*xint_i(c,s,d) = sum{i,IND, USE(c,s,i,d)*xint(c,s,i,d)};

Equation E_xtrad_r # Total demand for regional composite c,s in d #
(all,c,COM)(all,s,SRC)(all,d,DST)
  ID01[USE_U(c,s,d)]*xtrad_r(c,s,d) =
               USE_I(c,s,d)    *xint_i(c,s,d)
             + USE(c,s,"hou",d)*xhou(c,s,d)
             + USE(c,s,"inv",d)*xinv(c,s,d)
             + USE(c,s,"gov",d)*xgov(c,s,d)
             + USE(c,s,"exp",d)*xexp(c,s,d);

Coefficient ! Compute reporting variables xlocuse, xlocuse_s and xlocuse_sd !
(all,c,COM)(all,s,SRC)(all,d,DST)
   LOCUSE(c,s,d) # Non-export delivered value of regional composite c,s in d #;
(all,c,COM)(all,d,DST) LOCUSE_S(c,d)  # Non-export delivered value of good c #;
(all,c,COM)   LOCUSE_SD(c)   # Non-export national delivered value of good c #;
(all,c,COM)   IMPSHR(c)                     # Import share in non-export use #;
Formula
(all,c,COM)(all,s,SRC)(all,d,DST) LOCUSE(c,s,d) = USE_U(c,s,d)-USE(c,s,"exp",d);
(all,c,COM)(all,d,DST)            LOCUSE_S(c,d) = sum{s,SRC, LOCUSE(c,s,d)};
(all,c,COM)                       LOCUSE_SD(c)  = sum{d,DST, LOCUSE_S(c,d)};
(all,c,COM)   IMPSHR(c) = sum{d,DST,LOCUSE(c,"imp",d)}/LOCUSE_SD(c);
Write
 LOCUSE to file SUMMARY header "LUSE";
 IMPSHR to file SUMMARY header "MSHR";

Variable (all,c,COM)(all,s,SRC)(all,d,DST)
   xlocuse(c,s,d) # Total non-export demand for regional composite c,s in d#;
Equation E_xlocuse (all,c,COM)(all,s,SRC)(all,d,DST)
   ID01[USE_U(c,s,d)] *xtrad_r(c,s,d) =
   ID01[LOCUSE(c,s,d)]*xlocuse(c,s,d) + USE(c,s,"exp",d)*xexp(c,s,d);
Backsolve xlocuse using E_xlocuse;

Variable (all,c,COM)(all,d,DST)
           xlocuse_s(c,d) # Total non-export demand for composite good c in d #;
Equation E_xlocuse_s  (all,c,COM)(all,d,DST)
ID01[LOCUSE_S(c,d)]*xlocuse_s(c,d) = sum{s,SRC, LOCUSE(c,s,d)*xlocuse(c,s,d)};
Backsolve xlocuse_s using E_xlocuse_s;

Variable
(all,c,COM) xlocuse_sd(c) # National non-export demand for good c #;
Equation  E_xlocuse_sd (all,c,COM)
ID01[LOCUSE_SD(c)]*xlocuse_sd(c) = sum{d,DST, LOCUSE_S(c,d)*xlocuse_s(c,d)};

! Excerpt 19 of TABLO input file: !
! Delivering goods to regions !
Coefficient (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
         DELIVRD(c,s,r,d) # Trade + margins #;
Formula     (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
         DELIVRD(c,s,r,d)= TRADE(c,s,r,d) + sum{m,MAR, TRADMAR(c,s,m,r,d)};
Variable    (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
         pdelivrd(c,s,r,d) # All-user delivered price of good c,s from r to d #;
Coefficient
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST) BASSHR(c,s,r,d)
# Share of basic value in all-user delivered price #;
(all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST) MARSHR(c,s,m,r,d)
# Share of margin m in all-user delivered price #;
!Coefficient DEFSHR # default value for BASSHR and MARSHR #;
Formula     DEFSHR = 1/[1+sum{m,MAR,1}];!
Zerodivide default 0; ! alternative: DEFSHR !
Formula
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
 BASSHR(c,s,r,d) = TRADE(c,s,r,d)/DELIVRD(c,s,r,d);
(all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
 MARSHR(c,s,m,r,d) = TRADMAR(c,s,m,r,d)/DELIVRD(c,s,r,d);
Zerodivide off;

Variable
(all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
   atradmar(c,s,m,r,d) # Tech change: margin m on good c,s going from r to d #;
(all,m,MAR)(all,r,ORG)(all,d,DST)
   atradmar_cs(m,r,d) # Tech change: margin m on goods going from r to d #;

![[! for optional road-rail substitution!
Set ROADRAIL (TransRodo, TransFerro); ! edit these to match your names !
Subset ROADRAIL is subset of MAR;
Coefficient (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
 VRoadRail(c,s,r,d) # Road + rail cost #;
Formula     (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
 VRoadRail(c,s,r,d) = sum{m,ROADRAIL,TRADMAR(c,s,m,r,d)};
Coefficient (all,c,COM)(all,s,SRC)(all,m,ROADRAIL)(all,r,ORG)(all,d,DST)
 RRshr(c,s,m,r,d) # Road/rail share #;
Zerodivide default 0.5;
Formula     (all,c,COM)(all,s,SRC)(all,m,ROADRAIL)(all,r,ORG)(all,d,DST)
 RRshr(c,s,m,r,d) = TRADMAR(c,s,m,r,d)/VRoadRail(c,s,r,d);
Zerodivide off;
Coefficient (parameter) (all,c,COM) SIGROADRAIL(c) # Road/rail subst elast #;
!Formula     (all,c,COM) SIGROADRAIL(c) = 0.2;!
Read SIGROADRAIL from file REGSUPP header "SGRR";
Coefficient (all,m,MAR) IsRoadRail(m) # Binary dummy #;
Formula     (all,m,MAR) IsRoadRail(m) = 0;
       (all,m,ROADRAIL) IsRoadRail(m) = 1;
Variable    (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
 pRoadRail(c,s,r,d) # Ave efective road/rail price on good c,s going r to d #;
Equation E_pRoadRail
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
 pRoadRail(c,s,r,d) = sum{m,ROADRAIL,RRshr(c,s,m,r,d)*
                        [psuppmar_p(m,r,d)+atradmar(c,s,m,r,d)]};
Backsolve pRoadRail using E_pRoadRail; !]]!

Equation E_xtradmar # Leontief demand for margins #
 (all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
  xtradmar(c,s,m,r,d) =  xtrad(c,s,r,d) + atradmar(c,s,m,r,d)
! only for optional road-rail substitution:  
   - IsRoadRail(m)*SIGROADRAIL(c)*
          [psuppmar_p(m,r,d)+atradmar(c,s,m,r,d) - pRoadRail(c,s,r,d)] !;

Equation E_atradmar # Driver for margin tech change #
 (all,c,COM)(all,s,SRC)(all,m,MAR)(all,r,ORG)(all,d,DST)
  atradmar(c,s,m,r,d) =  atradmar_cs(m,r,d);
Backsolve atradmar using E_atradmar;

Equation E_pdelivrd # Delivered price of good c,s from r to d #
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST) pdelivrd(c,s,r,d) =
BASSHR(c,s,r,d)*pbasic(c,s,r)
 + sum{m,MAR, MARSHR(c,s,m,r,d)*[psuppmar_p(m,r,d)+atradmar(c,s,m,r,d)]};

! Excerpt 20 of TABLO input file: !
! For each good and destination region, an average user chooses region sourcing!
! based on delivered (margin-paid, but ex-tax) prices and values !
Coefficient (all,c,COM)(all,s,SRC)(all,d,DST)
  DELIVRD_R(c,s,d) # Demand in region d for delivered goods from all regions #;
Formula (all,c,COM)(all,s,SRC)(all,d,DST)
  DELIVRD_R(c,s,d) = sum{r,ORG,DELIVRD(c,s,r,d)};
Equation E_puse # Delivered price of regional composite good c,s to d #
 (all,c,COM)(all,s,SRC)(all,d,DST)
  ID01(DELIVRD_R(c,s,d))*puse(c,s,d) ! CES price index !
                      = sum{r,ORG,DELIVRD(c,s,r,d)*pdelivrd(c,s,r,d)};

Equation E_xtrad # CES between goods from different regions #
 (all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)
  xtrad(c,s,r,d) = xtrad_r(c,s,d)
                     - SIGMADOMDOM(c)*[pdelivrd(c,s,r,d)-puse(c,s,d)];

! alternative form with "twists":  
Variable (all,i,COM) (all,s,SRC)(all,k,ORG) twistsrc(i,s,k)
    # Sourcing twist towards origin (k) for good (i,s) #;
Equation E_xtrad # CES between goods from different regions #
(all,c,COM)(all,s,SRC)(all,r,ORG)(all,d,DST)  
xtrad(c,s,r,d)=xtrad_r(c,s,d)-SIGMADOMDOM(c)*[pdelivrd(c,s,r,d)-puse(c,s,d)]
 +  sum{k,ORG,  [if(k eq r,1) -
    DELIVRD(c,s,k,d)/{DELIVRD_R(c,s,d)+0.000000000001}]*twistsrc(c,s,k) }!

! Excerpt 21 of TABLO input file: !
! Where margins are produced !
! we need xSUPPMAR_P(m,r,d) margins .......what reg p shall they come from ? !
Coefficient
 (all,m,MAR)(all,r,ORG)(all,d,DST)
    TRADMAR_CS(m,r,d) # Total demand for margin m on goods from r to d #;
 (all,m,MAR)(all,r,ORG)(all,d,DST)    ! above should equal below !
    SUPPMAR_P(m,r,d)  # Total demand for margin m on goods from r to d #;
 (all,m,MAR)(all,r,ORG)(all,p,PRD)
    SUPPMAR_D(m,r,p)  # Total demand for margin m (from p) on goods from r #;
 (all,m,MAR)(all,p,PRD)
    SUPPMAR_RD(m,p)   # Total demand for margin m produced in p #;
 (all,m,MAR)(all,d,DST)(all,p,PRD)
    SUPPMAR_R(m,d,p) # Margins supplied by p on all goods passing to d #;
Formula
 (all,m,MAR)(all,r,ORG)(all,d,DST)
    TRADMAR_CS(m,r,d) = sum{c,COM, sum{s,SRC, TRADMAR(c,s,m,r,d)}};
 (all,m,MAR)(all,r,ORG)(all,d,DST)
    SUPPMAR_P(m,r,d)  = sum{p,PRD, SUPPMAR(m,r,d,p)};
 (all,m,MAR)(all,r,ORG)(all,p,PRD)
    SUPPMAR_D(m,r,p)  = sum{d,DST, SUPPMAR(m,r,d,p)};
 (all,m,MAR)(all,p,PRD)
    SUPPMAR_RD(m,p)   = sum{r,ORG, SUPPMAR_D(m,r,p)};
 (all,m,MAR)(all,d,DST)(all,p,PRD)
    SUPPMAR_R(m,d,p)  = sum{r,ORG, SUPPMAR(m,r,d,p)};

Equation E_xsuppmar_p # Composite margin m on goods passing from r to d #
 (all,m,MAR)(all,r,ORG)(all,d,DST) ! add up demands !
  ID01(TRADMAR_CS(m,r,d))*xsuppmar_p(m,r,d) =
   sum{c,COM, sum{s,SRC, TRADMAR(c,s,m,r,d)*xtradmar(c,s,m,r,d)}};

Variable (all,m,MAR)(all,r,ORG)(all,d,DST)(all,p,PRD) asuppmar(m,r,d,p)
# Tech change, Margin m supplied by p on goods passing from r to d #;

Equation E_psuppmar_p # Price of composite margin m on goods from r to d #
 (all,m,MAR)(all,r,ORG)(all,d,DST) ! CES price index !
   ID01(SUPPMAR_P(m,r,d))*psuppmar_p(m,r,d) =
      sum{p,PRD, SUPPMAR(m,r,d,p)*[pdom(m,p)+asuppmar(m,r,d,p)]};

Equation E_xsuppmar # Margin m supplied by p on goods passing from r to d #
 (all,m,MAR)(all,r,ORG)(all,d,DST)(all,p,PRD) ! CES demands !
  xsuppmar(m,r,d,p) = xsuppmar_p(m,r,d) + asuppmar(m,r,d,p)
           - SIGMAMAR(m)*[pdom(m,p)+asuppmar(m,r,d,p)-psuppmar_p(m,r,d)];

! add up total demand for margins produced in p !
Variable
(all,m,MAR)(all,r,ORG)(all,p,PRD)
   xsuppmar_d(m,r,p) # Total margins on goods from r, produced in p #;
Equation E_xsuppmar_d # Total margins on goods from r, produced in p #
(all,m,MAR)(all,r,ORG)(all,p,PRD)
ID01(SUPPMAR_D(m,r,p))*xsuppmar_d(m,r,p) =
 sum{d,DST, SUPPMAR(m,r,d,p)*xsuppmar(m,r,d,p)};

Equation E_xsuppmar_rd # Total demand for margins produced in p #
(all,m,MAR)(all,p,PRD) ID01(SUPPMAR_RD(m,p))*xsuppmar_rd(m,p) =
 sum{r,ORG, SUPPMAR_D(m,r,p)*xsuppmar_d(m,r,p)};

! Excerpt 22 of TABLO input file: !
! MAKE: multi-product industries have CET specification !
Coefficient
 (all,i,IND)(all,d,DST) MAKE_C(i,d) # All production by industry i #;
 (all,c,COM)(all,d,DST) MAKE_I(c,d) # Total production of commodities #;
Formula
 (all,i,IND)(all,d,DST) MAKE_C(i,d) = sum{c,COM, MAKE(c,i,d)};
 (all,c,COM)(all,d,DST) MAKE_I(c,d) = sum{i,IND, MAKE(c,i,d)};

! note: inventory accumulation is subtracted from industry output before
  industry output is transformed via MAKE into commodity output, 
ie,  MAKE_C(i,d) = VTOT(i,d) - STOCKS(i,d)   !
Variable 
 (all,i,IND)(all,d,DST) xmake_c(i,d) # Production, net of stocks #;
Equation E_xmake_c (all,i,IND)(all,d,DST)
  MAKE_C(i,d)*xmake_c(i,d) = VTOT(i,d)*xtot(i,d) - STOCKS(i,d)*xstocks(i,d);

Coefficient RECIPNCOM;
Formula     RECIPNCOM = 1/sum{c,COM,1};
Coefficient RECIPNIND;
Formula     RECIPNIND = 1/sum{i,IND,1};
Coefficient
(all,c,COM)(all,i,IND)(all,d,DST) MAKESHR1(c,i,d) # Com share in Ind output #;
(all,c,COM)(all,i,IND)(all,d,DST) MAKESHR2(c,i,d) # Ind share in Com supply #;
Zerodivide default RECIPNCOM;
Formula
(all,c,COM)(all,i,IND)(all,d,DST) MAKESHR1(c,i,d) = MAKE(c,i,d)/MAKE_C(i,d);
Zerodivide default RECIPNIND;
Formula
(all,c,COM)(all,i,IND)(all,d,DST) MAKESHR2(c,i,d) = MAKE(c,i,d)/MAKE_I(c,d);
Zerodivide off;
Variable
 (all,c,COM)(all,d,DST)  xcom(c,d) # Total output of commodities #;
Equation
 E_xmake  # Supplies of commodities by industries #
 (all,c,COM)(all,i,IND)(all,d,REG)
  xmake(c,i,d) = xmake_c(i,d) + SIGMAOUT(i)*[pmake(c,i,d)- ptot(i,d)];

! Remaining equations could most simply be written:!
!  E_xtot # Average price received by industries #
  (all,i,IND)(all,d,REG)  ID01(MAKE_C(i,d))*ptot(i,d)
   = sum{c,COM, MAKE(c,i,d)*pdom(c,d)};
  E_xcom # Total output of commodities (sum over ind) #
  (all,c,COM)(all,d,REG)  ID01(MAKE_I(c,d))*xcom(c,d)
   = sum{i,IND, MAKE(c,i,d)*xmake(c,i,d)}; !
! However, the more elaborate specification below gives considerable
computational savings where most sectors are single-product. It is designed
for use with the "IZ1 = no" (keep zeroes: re-use pivots) CMF option. !

Coefficient
 (all,c,COM)(all,i,IND) MAKE_D(c,i) # National MAKE matrix #;
 (integer,parameter)(all,c,COM) NINDPROD(c)# No of industries that make good c#;
 (integer,parameter)(all,i,IND) NCOMPROD(i)# No of commodities made by ind i #;
Formula
(all,c,COM)(all,i,IND) MAKE_D(c,i) = sum{d,DST, MAKE(c,i,d)};
(initial) (all,c,COM)  NINDPROD(c) = sum{i,IND:MAKE_D(c,i)<>0,1};
(initial) (all,i,IND)  NCOMPROD(i) = sum{c,COM:MAKE_D(c,i)<>0,1};
Set
 MIND # Multi-product industries #   = (all,i,IND:NCOMPROD(i)>1);
 SIND # Single-product industries #  =  IND - MIND;
 MINDCOM # Commodities produced by any industry in MIND #
     = (all,c,COM:sum{i,MIND,ABS[MAKE_D(c,i)]}>0);
Mapping SIND2COM from SIND to COM;
Formula (all,i,SIND) SIND2COM(i) = sum{c,COM:ABS[MAKE_D(c,i)]>0, $pos(c,COM)};

Equation
 E_xtotA # Average price received by multi-product industries #
  (all,i, MIND)(all,d,REG)
    ptot(i,d) = sum{c,MINDCOM, MAKESHR1(c,i,d)*pmake(c,i,d)};
 E_xtotB # Price received by single-product industries #
  (all,i,SIND)(all,d,REG) ptot(i,d) = pdom(SIND2COM(i),d);

Set
 MCOM # Commodities made by several industries #   = (all,c,COM:NINDPROD(c)>1);
 SCOM # Commodities made by only one industry #  =  COM - MCOM;
 MCOMIND # Industries that produce a commodity in MCOM #
     = (all,i,IND:sum{c,MCOM,ABS[MAKE_D(c,i)]}>0);
Mapping SCOM2IND from SCOM to IND;
Formula (all,c,SCOM) SCOM2IND(c) = sum{i,IND:ABS[MAKE_D(c,i)]>0, $pos(i,IND)};

Equation
 E_xcomA # Total output of commodities made by several industries #
  (all,c,MCOM)(all,d,REG)
   xcom(c,d) = sum{i,MCOMIND, MAKESHR2(c,i,d)*xmake(c,i,d)};
 E_xcomB # Output of commodities made by one industry #
  (all,c,SCOM)(all,d,REG)
   xcom(c,d) = xmake(c,SCOM2IND(c),d);

Write !debug output !
 (Set) MIND to file SUMMARY header "MIND";
 (Set) SIND to file SUMMARY header "SIND";
 (Set) MCOM to file SUMMARY header "MCOM";
 (Set) SCOM to file SUMMARY header "SCOM";
 (Set) MCOMIND to file SUMMARY header "MCI";
 (Set) MINDCOM to file SUMMARY header "MIC";
 (by_elements) SCOM2IND to file SUMMARY header "SC2I";
 (by_elements) SIND2COM to file SUMMARY header "SI2C";

! Next equation says that
 users of good c do not regard c made by different industries as perfect
 substitutes. Rather they CES (sigma=1/0.05) between the different industry
 products. This would be needed, for example, if 2 industries produced only
 electricity, with all inputs available elastically. The division of output
 between the two industries would not be well defined. Replace 0.05 by 0 to
 restore perfect substitute idea. !
Equation E_pmake  # Demands for commodities from industries #
 (all,c,COM)(all,i,IND)(all,d,REG)
!   xmake(c,i,d) = xcom(c,d) - 20*[pout(c,i,d) - pdom(c,d)]; !
    pmake(c,i,d) = pdom(c,d) - 0.05*[xmake(c,i,d) - xcom(c,d)];


! Excerpt 23 of TABLO input file: !
! Demand=supply for commodity c produced in r !
Coefficient        (all,c,COM)(all,s,SRC)(all,r,ORG) TRADE_D(c,s,r)
         # Total direct demands by ORG#;
Formula            (all,c,COM)(all,s,SRC)(all,r,ORG) TRADE_D(c,s,r)
         = sum{d,DST, TRADE(c,s,r,d)};
Variable           (all,c,COM)(all,s,SRC)(all,r,ORG) xtrad_d(c,s,r)
         # Total direct demands for goods produced(dom) or landed(imp) in r #;
Equation E_xtrad_d (all,c,COM)(all,s,SRC)(all,r,ORG)
ID01[TRADE_D(c,s,r)]*xtrad_d(c,s,r) = sum{d,DST, TRADE(c,s,r,d)*xtrad(c,s,r,d)};

Equation E_pdomA # Demand = supply for non-margins #
 (all,c,NONMAR)(all,r,REG) xcom(c,r) = xtrad_d(c,"dom",r);

Equation E_pdomB # Demand = supply for margins #
 (all,m,MAR)(all,p,REG) MAKE_I(m,p)*xcom(m,p) =
 TRADE_D(m,"dom",p)*xtrad_d(m,"dom",p) + SUPPMAR_RD(m,p)*xsuppmar_rd(m,p);

! Excerpt 24 of TABLO input file: !
! Aggregate final demand price and quantity indices !
Variable
(all,u,FINDEM)(all,d,DST) pfin(u,d) # Final user price indices #;
(all,u,FINDEM)(all,d,DST) xfin(u,d) # Final user quantity indices #;
(all,u,FINDEM)(all,d,DST) wfin(u,d) # Final user expenditures #;
Equation
E_pfin (all,u,FINDEM)(all,d,DST)
   ID01(PUR_CS(u,d))*pfin(u,d)=sum{c,COM,PUR_S(c,u,d)*ppur_s(c,u,d)};

E_xfina (all,d,DST) xfin("hou",d) = xhoutot(d);

E_xfinb (all,d,DST) ID01(PUR_CS("inv",d))*xfin("inv",d)=
           sum{c,COM, PUR_S(c,"inv",d)*xinv_s(c,d)};

E_xfinc (all,d,DST) ID01(PUR_CS("gov",d))*xfin("gov",d)=
           sum{c,COM, PUR_S(c,"gov",d)*xgov_s(c,d)};

E_xfind (all,d,DST) ID01(PUR_CS("exp",d))*xfin("exp",d)=
           sum{c,COM,sum{s,SRC, PUR(c,s,"exp",d)*xexp(c,s,d)}};

E_wfin (all,u,FINDEM)(all,d,DST)  wfin(u,d) = xfin(u,d)+ pfin(u,d);

Variable ! National final demand agregates !
(all,u,FINDEM) natpfin(u) # Final user price indices #;
(all,u,FINDEM) natxfin(u) # Final user quantity indices #;
(all,u,FINDEM) natwfin(u) # Final user expenditures #;
Equation
  E_natpfin (all,u,FINDEM) sum{d,DST,PUR_CS(u,d)*[natpfin(u)-pfin(u,d)]}=0;  
  E_natxfin (all,u,FINDEM) sum{d,DST,PUR_CS(u,d)*[natxfin(u)-xfin(u,d)]}=0;  
  E_natwfin (all,u,FINDEM) natwfin(u) = natpfin(u)+natxfin(u);

! Excerpt 25 of TABLO input file: !
! Commodity tax revenues !
Equation
 E_delTAXint (all,c,COM)(all,s,SRC)(all,i,IND)(all,d,DST)
  delTAXint(c,s,i,d) = 0.01*TAX(c,s,i,d)*[xint(c,s,i,d)+puse(c,s,d)]
                 + 0.01*PUR(c,s,i,d)*tuser(c,s,i,d);
 E_delTAXhou (all,c,COM)(all,s,SRC)(all,d,DST)
  delTAXhou(c,s,d)   = 0.01*TAX(c,s,"hou",d)*[xhou(c,s,d)+puse(c,s,d)]
                 + 0.01*PUR(c,s,"hou",d)*tuser(c,s,"hou",d);
 E_delTAXinv (all,c,COM)(all,s,SRC)(all,d,DST)
  delTAXinv(c,s,d)   = 0.01*TAX(c,s,"inv",d)*[xinv(c,s,d)+puse(c,s,d)]
                 + 0.01*PUR(c,s,"inv",d)*tuser(c,s,"inv",d);
 E_delTAXgov (all,c,COM)(all,s,SRC)(all,d,DST)
  delTAXgov(c,s,d)   = 0.01*TAX(c,s,"gov",d)*[xgov(c,s,d)+puse(c,s,d)]
                 + 0.01*PUR(c,s,"gov",d)*tuser(c,s,"gov",d);
 E_delTAXexp (all,c,COM)(all,s,SRC)(all,d,DST)
  delTAXexp(c,s,d)   = 0.01*TAX(c,s,"exp",d)*[xexp(c,s,d)+puse(c,s,d)]
                 + 0.01*PUR(c,s,"exp",d)*tuser(c,s,"exp",d);

! Excerpt 26 of TABLO input file: !
! Primary Factor Aggregates !
Coefficient
 (all,o,OCC)(all,d,DST) LAB_I(o,d) # Total wages by skill #;
 (all,d,DST) LAB_IO(d) # Total wages #;
 (all,d,DST) LND_I(d)  # Total rentals to land #;
 (all,d,DST) CAP_I(d)  # Total rentals to capital #;
 (all,o,OCC) LAB_ID(o) # Total wages by occupation #;
 (all,d,DST) PRIM_I(d) # Regional GDP at factor cost #;
Formula
 (all,o,OCC)(all,d,DST) LAB_I(o,d) = sum{i,IND, LAB(i,o,d)};
 (all,d,DST) LAB_IO(d) = sum{o,OCC, LAB_I(o,d)};
 (all,d,DST) LND_I(d)  = sum{i,IND, LND(i,d)};
 (all,d,DST) CAP_I(d)  = sum{i,IND, CAP(i,d)};
 (all,o,OCC) LAB_ID(o) = sum{d,DST, LAB_I(o,d)};
 (all,d,DST) PRIM_I(d) = sum{i,IND, PRIM(i,d)};
Variable
 (all,d,DST) xlnd_i(d)  # Aggregate land, rental-weighted #;
 (all,d,DST) plnd_i(d)  # Average land rent #;
 (all,d,DST) wlnd_i(d)  # Total rentals to land #;
 (all,d,DST) pprim_i(d) # Price index factor-cost GDP #;
 (all,d,DST) xcap_i(d)  # Aggregate capital, rental-weighted #;
 (all,d,DST) pcap_i(d)  # Average capital rent #;
 (all,d,DST) xprim_i(d) # Factor-cost GDP (includes only some tech change) #;
 (all,d,DST) wcap_i(d)  # Total rentals to capital #;
 (all,d,DST) wprim_i(d) # Total factor payments #;
Equation
E_xlnd_i (all,d,DST)
  LND_I(d)*xlnd_i(d) = sum{i,IND,LND(i,d)*xlnd(i,d)};
E_plnd_i (all,d,DST)
  LND_I(d)*plnd_i(d) = sum{i,IND,LND(i,d)*plnd(i,d)};
E_xcap_i (all,d,DST)
  CAP_I(d)*xcap_i(d) = sum{i,IND,CAP(i,d)*xcap(i,d)};
E_pcap_i (all,d,DST)
  CAP_I(d)*pcap_i(d) = sum{i,IND,CAP(i,d)*pcap(i,d)};
E_xprim_i (all,d,DST)
  PRIM_I(d)*xprim_i(d) = sum{i,IND,PRIM(i,d)*xprim(i,d)};
E_pprim_i (all,d,DST)
  PRIM_I(d)*pprim_i(d) = sum{i,IND,PRIM(i,d)*pprim(i,d)};
E_wlnd_i (all,d,DST)
  LND_I(d)*wlnd_i(d) = sum{i,IND,LND(i,d)*[plnd(i,d)+xlnd(i,d)]};
E_wcap_i (all,d,DST)
  CAP_I(d)*wcap_i(d) = sum{i,IND,CAP(i,d)*[pcap(i,d)+xcap(i,d)]};
E_wprim_i (all,d,DST)   wprim_i(d) = pprim_i(d) + xprim_i(d);

Variable 
 xlnd_id  # Aggregate land, rental-weighted #;
 plnd_id  # Average land rent #;
 xcap_id  # Aggregate capital, rental-weighted #;
 pcap_id  # Average capital rent #;
 xprim_id # Factor-cost GDP (includes only some tech change) #;
 pprim_id # Price index factor-cost GDP #;
Equation
 E_xlnd_id  sum{d,DST,  LND_I(d)*[ xlnd_i(d) -xlnd_id]}=0; 
 E_plnd_id  sum{d,DST,  LND_I(d)*[ plnd_i(d) -plnd_id]}=0; 
 E_xcap_id  sum{d,DST,  CAP_I(d)*[ xcap_i(d) -xcap_id]}=0;
 E_pcap_id  sum{d,DST,  CAP_I(d)*[ pcap_i(d) -pcap_id]}=0;
 E_xprim_id sum{d,DST, PRIM_I(d)*[xprim_i(d)-xprim_id]}=0;
 E_pprim_id sum{d,DST, PRIM_I(d)*[pprim_i(d)-pprim_id]}=0; 

! Sectoral contributions to xprim_i(d) [regional GDP at factor cost] !
Coefficient 
 (all,d,DST) RATIOPRIM_I(d) # Ratio (current/initial) real GDP at factor cost #;
Formula (initial)(all,d,DST) RATIOPRIM_I(d)= 1;
Coefficient (all,i,IND)(all,d,DST) PRIMSHR(i,d) # PRIM(i,d)/PRIM_I(d) #;
Formula     (all,i,IND)(all,d,DST) PRIMSHR(i,d) = PRIM(i,d)/PRIM_I(d);
! ORANI-G document, Excerpt 34 explains these decompositions !
Variable (change)(all,i,IND)(all,d,DST)
      contxprim_i(i,d) # Sector contributions to regional GDP at factor cost #;
Update (all,d,DST) RATIOPRIM_I(d)= xprim_i(d);
Equation
 E_contxprim_i (all,i,IND)(all,d,DST)
   contxprim_i(i,d) = RATIOPRIM_I(d)*PRIMSHR(i,d)*xprim(i,d);

! definition of real wages !
Variable
(all,i,IND)(all,o,OCC)(all,d,DST) realwage(i,o,d) # Wages deflated by CPI #;
Equation
E_plab (all,i,IND)(all,o,OCC)(all,d,DST)
  realwage(i,o,d) = plab(i,o,d) - pfin("Hou",d);

!####### sum-over-industry labour aggregates ##########!
Coefficient
 (all,i,IND)(all,o,OCC)(all,d,DST) SLAB_I(i,o,d) # Industry shares of wages #;
Formula
 (all,i,IND)(all,o,OCC)(all,d,DST) SLAB_I(i,o,d) = LAB(i,o,d)/LAB_I(o,d);
Variable
 (all,o,OCC)(all,d,DST) plab_i(o,d)     # Average wage by occ and reg #;
 (all,o,OCC)(all,d,DST) realwage_i(o,d) # Average real wages #;
 (all,o,OCC)(all,d,DST) xlab_i(o,d)     # Aggregate labour, wage-weighted #;
 (all,o,OCC)(all,d,DST) wlab_i(o,d)     # Total wage bill #;
 (all,o,OCC)(all,d,DST) rlab_i(o,d)     # Total real wage bill #;
Equation
E_plab_i (all,o,OCC)(all,d,DST)
  plab_i(o,d) = sum{i,IND, SLAB_I(i,o,d)*plab(i,o,d)};
E_realwage_i (all,o,OCC)(all,d,DST)
  realwage_i(o,d) = sum{i,IND, SLAB_I(i,o,d)*realwage(i,o,d)};
E_xlab_i (all,o,OCC)(all,d,DST)
  xlab_i(o,d) = sum{i,IND, SLAB_I(i,o,d)*xlab(i,o,d)};
E_wlab_i  (all,o,OCC)(all,d,DST)
  wlab_i(o,d) = xlab_i(o,d) + plab_i(o,d);
E_rlab_i  (all,o,OCC)(all,d,DST)
  rlab_i(o,d) = xlab_i(o,d) + realwage_i(o,d);

! Excerpt 27 of TABLO input file: !
! Sum-over-industry-and-region labour aggregates !

Coefficient
 (all,o,OCC)(all,d,DST) SLAB_ID(o,d) # Region shares of wages #;
Formula
 (all,o,OCC)(all,d,DST) SLAB_ID(o,d) = LAB_I(o,d)/LAB_ID(o);
Variable
 (all,o,OCC) plab_id(o)     # National average wage by occ #;
 (all,o,OCC) xlab_id(o)     # Aggregate labour by occ, wage-weighted #;
 (all,o,OCC) wlab_id(o)     # National wage bill by occ #;
 (all,o,OCC) realwage_id(o) # National average real wages by occ #;
 (all,o,OCC) rlab_id(o)     # National real wage bill by occ #;
Equation
E_plab_id (all,o,OCC)
  plab_id(o) = sum{d,DST, SLAB_ID(o,d)*plab_i(o,d)};
E_xlab_id (all,o,OCC)
  xlab_id(o) = sum{d,DST, SLAB_ID(o,d)*xlab_i(o,d)};
E_wlab_id (all,o,OCC)
  wlab_id(o) = xlab_id(o) + plab_id(o);
E_realwage_id (all,o,OCC)
  realwage_id(o) = sum{d,DST, SLAB_ID(o,d)*realwage_i(o,d)};
E_rlab_id (all,o,OCC)
  rlab_id(o) = xlab_id(o) + realwage_id(o);

! Excerpt 28 of TABLO input file: !
! Sum-over-occupation-and-industry labour aggregates !

Variable
(all,d,DST) plab_io(d)     # Average wage by region #;
(all,d,DST) xlab_io(d)     # Labour by region, wage-weighted #;
(all,d,DST) wlab_io(d)     # Total wage bill by region #;
(all,d,DST) rlab_io(d)     # Total real wage bill by region #;
(all,d,DST) realwage_io(d) # Average real wage by region #;
            plab_iod       # National average wage #; 
            xlab_iod       # National employment, wage-weighted #; 
Equation
E_plab_io  (all,d,DST)
  LAB_IO(d)*plab_io(d) = sum{o,OCC,LAB_I(o,d)*plab_i(o,d)};
E_xlab_io  (all,d,DST)
  LAB_IO(d)*xlab_io(d) = sum{o,OCC,LAB_I(o,d)*xlab_i(o,d)};
E_realwage_io  (all,d,DST)
  LAB_IO(d)*realwage_io(d) = sum{o,OCC,LAB_I(o,d)*realwage_i(o,d)};
E_wlab_io  (all,d,DST)
  wlab_io(d) = xlab_io(d) + plab_io(d);
E_rlab_io  (all,d,DST)
  rlab_io(d) = xlab_io(d) + realwage_io(d);
E_plab_iod  sum{d,DST, LAB_IO(d)*[plab_io(d) - plab_iod]} = 0;
E_xlab_iod  sum{d,DST, LAB_IO(d)*[xlab_io(d) - xlab_iod]} = 0;

! Excerpt 29 of TABLO input file: !
! Nominal Income-side GDP !

Set GDPINCCAT # GDP income categories # (LAND,LABOUR,CAPITAL,PRODTAX,COMTAX);
Coefficient (all,i,GDPINCCAT)(all,d,DST) GDPINCSUM(d,i)# Income GDP breakdown #;
Formula     (all,i,GDPINCCAT)(all,d,DST) GDPINCSUM(d,i) = 0;
(all,d,DST) GDPINCSUM(d,"Land")    = LND_I(d);
(all,d,DST) GDPINCSUM(d,"Labour")  = LAB_IO(d);
(all,d,DST) GDPINCSUM(d,"Capital") = CAP_I(d);
(all,d,DST) GDPINCSUM(d,"ProdTax") = sum{i,IND,PRODTAX(i,d)};
(all,d,DST) GDPINCSUM(d,"ComTax")  =
              sum{u,USR, sum{c,COM,sum{s,SRC,TAX(c,s,u,d)}}};
Write GDPINCSUM to file SUMMARY header "GNSM";

Coefficient (all,d,DST) GDPINC(d)# Income GDP #;
Formula     (all,d,DST) GDPINC(d) = sum{i,GDPINCCAT, GDPINCSUM(d,i)};

Variable (change) (all,i,GDPINCCAT)(all,d,DST)
          delGDPINC(d,i)# Ordinary change in nominal income GDP component #;
Equation
 E_delGDPINCa (all,d,DST) delGDPINC(d,"Land")
    = 0.01*LND_I(d)*wlnd_i(d);
 E_delGDPINCb (all,d,DST) delGDPINC(d,"Capital")
    = 0.01*CAP_I(d)*wcap_i(d);
 E_delGDPINCc (all,d,DST) delGDPINC(d,"Labour")
    = 0.01*LAB_IO(d)*wlab_io(d);
 E_delGDPINCd (all,d,DST) delGDPINC(d,"ProdTax") = sum{i,IND,delPTX(i,d)};
 E_delGDPINCe (all,d,DST) delGDPINC(d,"ComTax") = sum{c,COM,sum{s,SRC,
 sum{i,IND,delTAXint(c,s,i,d)}+
  delTAXhou(c,s,d)+delTAXinv(c,s,d)+delTAXgov(c,s,d)+delTAXexp(c,s,d)}};

Variable  (all,d,DST) wgdpinc(d) # Nominal income GDP #;
Equation E_wgdpinc
 (all,d,DST) GDPINC(d)*wgdpinc(d) = 100*sum{i,GDPINCCAT, delGDPINC(d,i)};

! Excerpt 30 of TABLO input file: !
! Principal components of real and nominal expenditure side GDP !

Coefficient (all,c,COM)(all,s,SRC)(all,d,DST)
     TRADE_R(c,s,d) # Total direct demands by DST#;
Formula     (all,c,COM)(all,s,SRC)(all,d,DST)
     TRADE_R(c,s,d) = sum{r,ORG, TRADE(c,s,r,d)};

Set GDPEXPCAT (HOU, INV, GOV, STOCKS, EXP, IMPORTS, REXPORTS, RIMPORTS, NETMAR);
Subset FINDEM is subset of GDPEXPCAT;
Coefficient (all,i,GDPEXPCAT)(all,q,DST) GDPEXPSUM(q,i) # Expend GDP breakdown#;
Formula     (all,i,GDPEXPCAT)(all,q,DST) GDPEXPSUM(q,i) = 0;
(all,q,DST)(all,f,FINDEM) GDPEXPSUM(q,f) = sum{c,COM, PUR_S(c,f,q)};
(all,q,DST) GDPEXPSUM(q,"Stocks") = sum{i,IND, STOCKS(i,q)};
(all,q,REG) GDPEXPSUM(q,"Imports") =
     -sum{c,COM, sum{d,DST, TRADE(c,"imp",q,d)}};
(all,q,REG) GDPEXPSUM(q,"NetMar") = sum{m,MAR, sum{r,ORG,
       SUPPMAR_D(m,r,q) - SUPPMAR_P(m,r,q) }};
(all,q,REG) GDPEXPSUM(q,"Rexports") =
     sum{c,COM,sum{s,SRC, TRADE_D(c,s,q) - TRADE(c,s,q,q)}};
(all,q,REG) GDPEXPSUM(q,"Rimports") =
    - sum{c,COM,sum{s,SRC, TRADE_R(c,s,q) - TRADE(c,s,q,q)}};

Write GDPEXPSUM to file SUMMARY header "GESM";

Coefficient (all,d,DST) GDPEXP(d)# Expenditure GDP #;
Formula     (all,d,DST) GDPEXP(d) = sum{i,GDPEXPCAT, GDPEXPSUM(d,i)};

Variable (change) (all,i,GDPEXPCAT)(all,d,DST)
      delXGDPEXP(d,i)# Ordinary change in quantity expenditure GDP component #;
Equation
 E_delXGDPEXPa (all,d,DST)(all,u,FINDEM) delXGDPEXP(d,u)
    = 0.01*PUR_CS(u,d)*xfin(u,d);
 E_delXGDPEXPb (all,d,DST) delXGDPEXP(d,"Stocks")
    = 0.01*sum{i,IND,STOCKS(i,d)*xstocks(i,d)};
 E_delXGDPEXPc (all,q,REG) delXGDPEXP(q,"Imports")
    =-0.01*sum{c,COM, TRADE_D(c,"imp",q)*xtrad_d(c,"imp",q)};
E_delXGDPEXPd (all,q,REG) delXGDPEXP(q,"NetMar")
    = 0.01*sum{m,MAR, sum{r,ORG,
        sum{d,DST, SUPPMAR(m,r,d,q)*xsuppmar(m,r,d,q)}
      - sum{p,PRD, SUPPMAR(m,r,q,p)*xsuppmar(m,r,q,p)}  }};
E_delXGDPEXPe (all,q,REG) delXGDPEXP(q,"Rexports")
    = 0.01*sum{c,COM, sum{s,SRC,
        TRADE_D(c,s,q)*xtrad_d(c,s,q)
                 - TRADE(c,s,q,q)*xtrad(c,s,q,q)  }};
E_delXGDPEXPf (all,q,REG) delXGDPEXP(q,"Rimports")
    =-0.01*sum{c,COM, sum{s,SRC,
          TRADE_R(c,s,q)*xtrad_r(c,s,q)
                 - TRADE(c,s,q,q)*xtrad(c,s,q,q)  }};

Variable (all,d,DST)(all,i,GDPEXPCAT) 
     xXGDPEXP(d,i)# Percent change in quantity expenditure GDP component #;
Equation E_xXGDPEXP (all,d,DST)(all,i,GDPEXPCAT)
  ID01[GDPEXPSUM(d,i)]*xXGDPEXP(d,i)= 100*delXGDPEXP(d,i);

Variable  (all,d,DST) xgdpexp(d) # Real expenditure GDP #;
Equation E_xgdpexp
 (all,d,DST) GDPEXP(d)*xgdpexp(d) = 100*sum{i,GDPEXPCAT, delXGDPEXP(d,i)};

Variable (change) (all,i,GDPEXPCAT)(all,d,DST)
        delPGDPEXP(d,i)# Ordinary change in price expenditure GDP component #;
Equation
 E_delPGDPEXPa (all,d,DST)(all,u,FINDEM) delPGDPEXP(d,u)
    = 0.01*PUR_CS(u,d)*pfin(u,d);
 E_delPGDPEXPb (all,d,DST) delPGDPEXP(d,"Stocks")
    = 0.01*sum{i,IND, STOCKS(i,d)*ptot(i,d)};
 E_delPGDPEXPc (all,q,REG) delPGDPEXP(q,"Imports")
    =-0.01*sum{c,COM, TRADE_D(c,"imp",q)*pimp(c,q)};
E_delPGDPEXPd (all,q,REG) delPGDPEXP(q,"NetMar")
    = 0.01*sum{m,MAR, SUPPMAR_RD(m,q)*pdom(m,q)
        -  sum{p,PRD, SUPPMAR_R(m,q,p)*pdom(m,p)}};
E_delPGDPEXPe (all,q,REG) delPGDPEXP(q,"Rexports") = 0.01*sum{c,COM,sum{s,SRC,
    [TRADE_D(c,s,q) - TRADE(c,s,q,q)]*pbasic(c,s,q)}};
E_delPGDPEXPf (all,q,REG) delPGDPEXP(q,"Rimports") =-0.01*sum{c,COM,sum{s,SRC,
    sum{r,ORG, TRADE(c,s,r,q)*pbasic(c,s,r)} - TRADE(c,s,q,q)*pbasic(c,s,q)}};

Variable (change) (all,i,GDPEXPCAT)(all,d,DST)
        delWGDPEXP(d,i)# Ordinary change in nominal expenditure GDP component #;
Equation E_delWGDPEXP (all,i,GDPEXPCAT)(all,d,DST)
        delWGDPEXP(d,i) = delXGDPEXP(d,i) + delPGDPEXP(d,i);

Variable  (all,d,DST) pgdpexp(d) # Price index expenditure GDP #;
Equation E_pgdpexp
 (all,d,DST) GDPEXP(d)*pgdpexp(d) = 100*sum{i,GDPEXPCAT, delPGDPEXP(d,i)};

Variable  (all,d,DST) wgdpexp(d) # Nominal expenditure GDP #;
Equation E_wgdpexp   (all,d,DST) wgdpexp(d) = xgdpexp(d)+pgdpexp(d);

Variable  (all,d,DST) wgdpdiff(d) # SHOULD=0: nominal (income - expend) GDP #;
Equation E_wgdpdiff (all,d,DST) wgdpdiff(d) = wgdpinc(d)- wgdpexp(d);

Coefficient (all,d,REG) RATIOGDPEXP(d)# (Current/initial) real expenditure GDP#;
Formula  (initial)(all,d,REG) RATIOGDPEXP(d) = 1;
Update            (all,d,REG) RATIOGDPEXP(d) = xgdpexp(d);

Variable (change) (all,i,GDPEXPCAT)(all,d,REG) contxgdpexp(i,d)
 # Contributions to % regional real GDP expenditure #;
Equation E_contxgdpexp (all,i,GDPEXPCAT)(all,d,REG)
   GDPEXP(d)*contxgdpexp(i,d) = 100*RATIOGDPEXP(d)*delXGDPEXP(d,i);

! Excerpt 31 of TABLO input file: !
! Indices and components of national GDP expend  !

Set NATGDPEXPCAT (HOU, INV, GOV, STOCKS, EXP, Imports);
Subset NATGDPEXPCAT is subset of GDPEXPCAT;
Subset FINDEM is subset of NATGDPEXPCAT;

Coefficient (all,i,NATGDPEXPCAT) NATGDPEXPSUM(i) # Expend GDP breakdown#;
Formula (all,i,NATGDPEXPCAT) NATGDPEXPSUM(i)=sum{q,DST, GDPEXPSUM(q,i)};
Write NATGDPEXPSUM to file SUMMARY header "NGSM";

Coefficient NATGDPEXP # Expenditure GDP #;
Formula     NATGDPEXP = sum{d,DST, GDPEXP(d)};

Variable (change) (all,i,NATGDPEXPCAT)
      delNatXGDPEXP(i)# Ordinary change in quantity expenditure GDP component #;
Equation E_delNatXGDPEXP  (all,i,NATGDPEXPCAT)
    delNatXGDPEXP(i) = sum{d,DST, delXGDPEXP(d,i)};

Variable (all,i,NATGDPEXPCAT)
     xNatXGDPEXP(i) # Percent change in quantity expenditure GDP component #;
Equation E_xNatXGDPEXP (all,i,NATGDPEXPCAT)
  ID01[NATGDPEXPSUM(i)]*xNatXGDPEXP(i)= 100*delNatXGDPEXP(i);

Variable   natxgdpexp # Real expenditure GDP #;
Equation E_natxgdpexp
  NATGDPEXP*natxgdpexp  = 100*sum{i,NATGDPEXPCAT, delNatXGDPEXP(i)};

Variable (change) (all,i,NATGDPEXPCAT)
       delNatPGDPEXP(i)# Ordinary change in price expenditure GDP component #;
Equation E_delNatPGDPEXP  (all,i,NATGDPEXPCAT)
      delNatPGDPEXP(i) = sum{d,DST, delPGDPEXP(d,i)};

Variable (all,i,NATGDPEXPCAT)
     pNatXGDPEXP(i) # Percent change in price expenditure GDP component #;
Equation E_pNatXGDPEXP (all,i,NATGDPEXPCAT)
  ID01[NATGDPEXPSUM(i)]*pNatXGDPEXP(i)= 100*delNatPGDPEXP(i);

Variable  natpgdpexp # Price index expenditure national GDP #;
Equation E_natpgdpexp
  NATGDPEXP*natpgdpexp = 100*sum{i,NATGDPEXPCAT, delNatPGDPEXP(i)};

Variable   natwgdpexp # Nominal expenditure GDP #;
Equation E_natwgdpexp   natwgdpexp = natxgdpexp + natpgdpexp;

Variable (change) (all,i,NATGDPEXPCAT) contxnatgdpexp(i)
 # Contributions to % change in national real GDP expenditure #;
Coefficient  RATIONGDPEXP# (Current/initial) real expenditure GDP#;
Formula  (initial) RATIONGDPEXP = 1;
Update             RATIONGDPEXP = natxgdpexp;
Equation E_contxnatgdpexp (all,i,NATGDPEXPCAT)
   NATGDPEXP*contxnatgdpexp(i) = 100*RATIONGDPEXP*delNatXGDPEXP(i);

Set GNECAT # parts of GNE # (HOU, INV, GOV, STOCKS);
Subset GNECAT is subset of NATGDPEXPCAT;
Variable 
 natxgne # national real GNE #;
 natpgne # national GNE price index #;
 natwgne # national nominal GNE #;
Equation 
 E_natxgne sum{i,GNECAT, NATGDPEXPSUM(i)*[natxgne-xNatXGDPEXP(i)]}=0;
 E_natpgne sum{i,GNECAT, NATGDPEXPSUM(i)*[natpgne-pNatXGDPEXP(i)]}=0;
 E_natwgne natwgne = natxgne + natpgne;

Variable natwgdpinc # Nominal income GDP #;
Equation E_natwgdpinc sum{d,DST, GDPINC(d)*[wgdpinc(d)-natwgdpinc]} = 0;

Variable  natwgdpdiff  # SHOULD=0: nominal (income - expend) GDP #;
Equation E_natwgdpdiff  natwgdpdiff  = natwgdpinc - natwgdpexp;

! Excerpt 32 of TABLO input file: !
! Regional macro reporting variables !
Coefficient
(all,s,SRC)(all,d,DST) TRADE_CR(s,d) # Total direct demands dest d #;
Formula
(all,s,SRC)(all,d,DST) TRADE_CR(s,d) = sum{c,COM, TRADE_R(c,s,d)};

Coefficient (all,d,DST) IMPUSED_C(d) # Imports used in d #;
Formula     (all,d,DST) IMPUSED_C(d) = TRADE_CR("imp",d);
Variable
 (all,c,COM)(all,d,DST) ximps(c,d) # Volume of imports used in d #;
 (all,d,DST) ximpused(d) # Volume of imports used in d #;
 (all,d,DST) pimpused(d) # Price index, imports used in d #;
Equation E_ximps (all,c,COM)(all,d,DST) ximps(c,d) =xtrad_r(c,"imp",d);

Equation E_ximpused (all,d,DST) IMPUSED_C(d)*ximpused(d) =
              sum{c,COM,TRADE_R(c,"imp",d)*ximps(c,d)};
Equation E_pimpused (all,d,DST) IMPUSED_C(d)*pimpused(d) =
              sum{c,COM, sum{r,ORG,TRADE(c,"imp",r,d)*pimp(c,r)}};

Coefficient (all,r,ORG) IMPLANDED_C(r) # Imports landed in r #;
Formula     (all,r,ORG) IMPLANDED_C(r) = sum{c,COM, TRADE_D(c,"imp",r)};
Variable
 (all,r,ORG) ximplanded(r) # Volume of imports landed in d #;
 (all,r,ORG) pimplanded(r) # Price index, imports landed in d #;
Equation E_ximplanded (all,r,ORG) ID01[IMPLANDED_C(r)]*ximplanded(r) =
              sum{c,COM,TRADE_D(c,"imp",r)*xtrad_d(c,"imp",r)};
Equation E_pimplanded (all,r,ORG) ID01[IMPLANDED_C(r)]*pimplanded(r) =
              sum{c,COM,TRADE_D(c,"imp",r)*pimp(c,r)};

Set MAINMACROS # Convenient macros for reporting #
 (RealHou, RealInv, RealGov, ExpVol, ImpVolUsed, ImpsLanded, RealGDP, AggEmploy,
  realwage_io, plab_io, AggCapStock,GDPPI, CPI,ExportPI, ImpsLandedPI,
  Population, NomHou, NomGDP);
Variable
(all,m,MAINMACROS)(all,q,REG) MainMacro(m,q) # Convenient macros for reporting#;
Equation
 E_MainMacroA (all,q,REG) MainMacro("RealHou",q)     = xfin("Hou",q);
 E_MainMacroB (all,q,REG) MainMacro("RealInv",q)     = xfin("Inv",q);
 E_MainMacroC (all,q,REG) MainMacro("RealGov",q)     = xfin("Gov",q);
 E_MainMacroD (all,q,REG) MainMacro("ExpVol",q)      = xfin("Exp",q);
 E_MainMacroE (all,q,REG) MainMacro("ImpVolUsed",q)  = ximpused(q);
 E_MainMacroF (all,q,REG) MainMacro("ImpsLanded",q)  = ximplanded(q);
 E_MainMacroG (all,q,REG) MainMacro("RealGDP",q)     = xgdpexp(q);
 E_MainMacroH (all,q,REG) MainMacro("AggEmploy",q)   = xlab_io(q);
 E_MainMacroI (all,q,REG) MainMacro("realwage_io",q) = realwage_io(q);
 E_MainMacroJ (all,q,REG) MainMacro("plab_io",q)     = plab_io(q);
 E_MainMacroK (all,q,REG) MainMacro("AggCapStock",q) = xcap_i(q);
 E_MainMacroL (all,q,REG) MainMacro("CPI",q)         = pfin("Hou",q);
 E_MainMacroM (all,q,REG) MainMacro("GDPPI",q)       = pgdpexp(q);
 E_MainMacroN (all,q,REG) MainMacro("ExportPI",q)    = pfin("Exp",q);
 E_MainMacroO (all,q,REG) MainMacro("ImpsLandedPI",q)= pimplanded(q);
 E_MainMacroP (all,q,REG) MainMacro("Population",q)  = nhou(q);
 E_MainMacroQ (all,q,REG) MainMacro("NomHou",q)      = wfin("Hou",q);
 E_MainMacroR (all,q,REG) MainMacro("NomGDP",q)      = wgdpexp(q);

! Calculation of Macros for broad regions (optional, but useful for Brazil)  
Set AGGREG # Broad regions # read elements from file REGSUPP header "AREG";
Mapping MAPREG from REG to AGGREG;
Read (by_elements) MAPREG from file REGSUPP header "MREG";
Variable (all,m,MAINMACROS)(all,a,AGGREG) ZoneMacro(m,a) # Broad region macros#;
Equation E_ZoneMacro (all,m,MAINMACROS)(all,a,AGGREG)
sum{q,REG:MAPREG(q)=a, WMAINMACRO(m,q)*[ZoneMacro(m,a)-MainMacro(m,q)]} = 0;!

! Excerpt 33 of TABLO input file: !
! Aggregation of regional macro variables to national macro variables !
Coefficient
 (all,m,MAINMACROS)(all,q,REG) WMAINMACRO(m,q) # Weights to aggregate macros #;
 (all,m,MAINMACROS) WNATMACRO(m) # Total of WMainMacro #;
 (all,m,MAINMACROS) RATIOMMACRO(m) # Initial/final ratio #;
Formula
 (all,q,REG) WMAINMACRO("RealHou",q)     = PUR_CS("Hou",q);
 (all,q,REG) WMAINMACRO("RealInv",q)     = PUR_CS("Inv",q);
 (all,q,REG) WMAINMACRO("RealGov",q)     = PUR_CS("Gov",q);
 (all,q,REG) WMAINMACRO("ExpVol",q)      =  PUR_CS("Exp",q);
 (all,q,REG) WMAINMACRO("ImpVolUsed",q)  = IMPUSED_C(q);
 (all,q,REG) WMAINMACRO("ImpsLanded",q)  = IMPLANDED_C(q);
 (all,q,REG) WMAINMACRO("RealGDP",q)     = GDPEXP(q);
 (all,q,REG) WMAINMACRO("AggEmploy",q)   = LAB_IO(q);
 (all,q,REG) WMAINMACRO("realwage_io",q) = LAB_IO(q);
 (all,q,REG) WMAINMACRO("plab_io",q)     = LAB_IO(q);
 (all,q,REG) WMAINMACRO("AggCapStock",q) = CAP_I(q);
 (all,q,REG) WMAINMACRO("CPI",q)         = PUR_CS("Hou",q);
 (all,q,REG) WMAINMACRO("GDPPI",q)       = GDPEXP(q);
 (all,q,REG) WMAINMACRO("ExportPI",q)    = PUR_CS("Exp",q);
 (all,q,REG) WMAINMACRO("ImpsLandedPI",q)= IMPLANDED_C(q);
 (all,q,REG) WMAINMACRO("Population",q)  = POP(q);
 (all,q,REG) WMAINMACRO("NomHou",q)      = PUR_CS("Hou",q);
 (all,q,REG) WMAINMACRO("NomGDP",q)      = GDPEXP(q);
 (all,m,MAINMACROS) WNATMACRO(m) = sum{q,REG, WMAINMACRO(m,q)};
 (initial)(all,m,MAINMACROS) RATIOMMACRO(m) = 1;

Variable
(all,m,MAINMACROS) NatMacro(m) # National macros for reporting #;
(change)(all,m,MAINMACROS)(all,q,REG)
   contMainMacro(m,q) # Regional contributions to national macro results #;
Update (all,m,MAINMACROS) RATIOMMACRO(m) = NatMacro(m);
Equation E_NatMacro (all,m,MAINMACROS)
   WNATMACRO(m)*NatMacro(m) =  sum{q,REG,WMAINMACRO(m,q)*MainMacro(m,q)};
Equation E_contMainMacro (all,m,MAINMACROS)(all,q,REG)
   WNATMACRO(m)*contMainMacro(m,q)
      =  RATIOMMACRO(m)*WMAINMACRO(m,q)*MainMacro(m,q);

Variable (change) shrBoT # National real balance of trade as % of real GDP #;
! S=100B/G -> SG=100B -> SdG+GdS=100dB -> SGg/100+GdS=100dB -> Bg+GdS=100dB !
Equation E_shrBoT
[WNATMACRO("ExpVol")-WNATMACRO("ImpsLanded")]*NatMacro("RealGDP")
+ WNATMACRO("RealGDP")*shrBoT = WNATMACRO("ExpVol")*NatMacro("ExpVol")
                              - WNATMACRO("ImpsLanded")*NatMacro("ImpsLanded");

Variable ! shrBoTnom may be chosen as exogenous for trade balance constraint !
 (change) shrBoTnom # National nominal balance of trade as % of nominal GDP #;
! S=100B/G -> SG=100B -> SdG+GdS=100dB -> SGg/100+GdS=100dB -> Bg+GdS=100dB !
Equation E_shrBoTnom
[WNATMACRO("ExpVol")-WNATMACRO("ImpsLanded")]*NatMacro("NomGDP")
+ WNATMACRO("NomGDP")*shrBoTnom =
  WNATMACRO("ExpVol")*[NatMacro("ExpVol")+NatMacro("ExportPI")]
- WNATMACRO("ImpsLanded")*[NatMacro("ImpsLanded")+NatMacro("ImpsLandedPI")];

Variable prealdev # real devaluation = imports PI - GDP PI #;
Equation E_prealdev prealdev = NatMacro("ImpsLandedPI") - NatMacro("GDPPI");

Variable ptoft # Terms of trade = exports PI - imports PI #;
Equation E_ptoft ptoft = NatMacro("ExportPI") - NatMacro("ImpsLandedPI");

! Excerpt 34 of TABLO input file: !
! Labour market closure !

! for each  OCC, choose ONE of these 6 (3 x 2) closures:
For NATIONAL labour supply, choose one of: 
1: national employment is fixed, national real wage varies
     xlab_id exogenous; realwage_id and flabsup_id endogenous
2: national real wage  is fixed, national supply elastic
     realwage_id exogenous; xlab_id and flabsup_id endogenous
3: national employment is positive function of national real wage
     flabsup_id exogenous; xlab_id and realwage_id endogenous
!
Variable (all,o,OCC) flabsup_id(o)  # National wage shifter #;
Equation E_labslack # National labour supply #
 (all,o,OCC) ! 0.5 represents a national labour supply elasticity value !
  realwage_id(o) = [1/0.5]*xlab_id(o) + flabsup_id(o);
! nb above equation does nothing if flabsup_id endogenous !

!For REGIONAL labour supply, choose either:
A: inter-regional labour migration driven by wage differentials between regions
    [consistent with a national supply of each OCC CET-ting between regions ]
B: no inter-regional wage differentials  !
Variable
 (all,o,OCC) labslack(o) # Slack to allow occ-specific employment constraint #;
 (all,o,OCC)(all,d,DST) flabsupA(o,d) # Labour migration shifter #;
 (all,o,OCC)(all,d,DST) flabsupB(o,d) # Labour migration shifter #;
Equation E_flab_i # Inter-regional labour migration or labour supply #
 (all,o,OCC)(all,d,DST) ! 1.1 represents a regional labour supply elasticity !
 realwage_i(o,d) = [1/1.1]*xlab_i(o,d) + flabsupA(o,d) + labslack(o);
! for each OCC, choose ONE of flabsupA or flabsupB exogenous !
Equation E_flabsupB # No inter-regional wage differentials #
 (all,o,OCC)(all,d,DST) realwage_i(o,d) = flabsupB(o,d) + labslack(o);

! rule for awarding extra wages to certain industries !
Variable
(all,i,IND)(all,o,OCC)(all,d,DST) flab(i,o,d) # Wage shifter #;
           (all,o,OCC)(all,d,DST) flab_i(o,d) # Wage shifter #;
                      (all,d,DST) flab_io(d)  # Wage shifter #;
                                  flab_iod    # National wage shifter #;
                      (all,o,OCC) flab_id(o)  # National wage shifter #;

Equation E_realwage  # Wage fixing rule #
 (all,i,IND)(all,o,OCC)(all,d,DST)
 realwage(i,o,d) = flab(i,o,d) +flab_i(o,d) +flab_io(d) +flab_id(o) +flab_iod;
! usually realwage_i(o,d) will be determined by previous equations
  so flab_i(o,d) will be endogenous !

! Automatic closure for above will give
                Variable     Size 
Exogenous           flab ;  IND*OCC*DST   Wage shifter
Exogenous       flabsupA ;  OCC*DST   Labour migration shifter
Exogenous     flabsup_id ;  OCC   National wage shifter
Exogenous        flab_id ;  OCC   National wage shifter
Exogenous        flab_io ;  DST   Wage shifter
Exogenous       flab_iod ;  1   National wage shifter 

Above implements choice 3A above
   [national labour supply curve plus regional wage differentials]
Typical long-run swaps might be (newly exog at left):
  swap xlab_id = flabsup_id;   ! ! fix national employment
For a very shortrun closure (all real wages fixed), you could choose:
  swap flab_i = flabsupA; ! ! switch off regional wage differentials
  swap labslack = flabsup_id; ! ! switch off national labour supply mechanism

Of course, you need not choose the same options for ALL occs !

! Excerpt 35 of TABLO input file: !
! Mechanism to allow fixed total capital to flow between sectors !

Variable
 (all,i,IND)(all,d,DST) fgret(i,d)
              # Shifter to lock together industry rates of return #;
             capslack # Slack variable to allow fixing aggregate capital #;
Equation E_fgret # Equation to force rates of return to move together #
 (all,i,IND)(all,d,DST) gret(i,d) = fgret(i,d) + capslack;
! Normally, capslack is exogenous and fgret endogenous, so above just
  determines fgret. To allow capital to be mobile between sectors, yet 
  fixed in aggregate, swap [newly exogenous at left]:
    xcap_id   with   capslack     
    fgret     with   xcap         !

! Excerpt 36 of TABLO input file: !
! National and regional consumption functions !

Variable natfhou # National ratio, nominal household consumption to GDP #;
Equation E_natfhou   NatMacro("NomHou") = natfhou + NatMacro("NomGDP");

Coefficient
 (all,o,OCC)(all,h,HOU)(all,d,DST) HOULABINC(o,d,h) # Wage income matrix #;
Read HOULABINC  from file INFILE header "WINC";
Update (all,o,OCC)(all,h,HOU)(all,d,DST) HOULABINC(o,d,h) = wlab_i(o,d);
Write
 HOULABINC to file SUMMARY header "WINC";
 LAB to file SUMMARY header "1LAB";

Variable
(all,h,HOU)(all,d,DST) wlabh(h,d) # Total wage bill by region and household #;
Equation E_wlabh (all,h,HOU)(all,d,DST)
sum{o,OCC,HOULABINC(o,d,h)*[wlab_i(o,d)-wlabh(h,d)]}=0;

Variable ! Regional Consumption function !
 (all,h,HOU)(all,d,DST) fhou(h,d)
 # Regional propensity to consume from labour income #;
  houslack # Consumption slack variable to accommodate national constraint #;
Equation E_fhou (all,h,HOU)(all,d,DST)
    whouhtot(h,d)  =  wlabh(h,d) + fhou(h,d) + houslack;

Variable (all,d,REG) fgovtot2(d) # Government demand shifter #;
Equation E_fgovtot2
 (all,d,REG) fgovtot(d) = fgovtot2(d) +  MainMacro("RealHou",d);
! See equation E_xgov: with fgovtot2 exogenous, fgovtot endogenous, regional
real government spending demand follows regional real household demand !

! Excerpt 37 of TABLO input file: !
! Exports to ROW by region of production !
! Strictly speaking, the concept of "Exports by region of production" is not
  part of the TERM model. However, we can estimate them by making additional
  common-sense assumptions [source share for exports is same as for overall
  demand !
Coefficient
(all,c,COM)(all,r,ORG)(all,d,DST) ROWDEM(c,r,d) # Eventually exported goods #;
(all,c,COM)(all,r,ORG) ROWDEM_D(c,r) # Eventually exported goods by origin #;
(all,c,COM)(all,r,REG) EXPSHR(c,r) # Share of output eventually exported #;
Formula
(all,c,COM)(all,r,ORG)(all,d,DST) ROWDEM(c,r,d)
   = TRADE(c,"dom",r,d)*USE(c,"dom","Exp",d)/ID01[USE_U(c,"dom",d)];
(all,c,COM)(all,r,ORG) ROWDEM_D(c,r) = sum{d,DST, ROWDEM(c,r,d)};
(all,c,COM)(all,r,REG) EXPSHR(c,r) = ROWDEM_D(c,r)/[0.001+MAKE_I(c,r)];

Write ROWDEM to file SUMMARY header "ROWD";
Write EXPSHR to file SUMMARY header "SHXP";

Variable
(all,c,COM)(all,r,ORG)(all,d,DST) xrowdem(c,r,d) # Eventually exported goods #;
Equation E_xrowdem (all,c,COM)(all,r,ORG)(all,d,DST)
  xrowdem(c,r,d) = xexp(c,"dom",d) - xtrad_r(c,"dom",d) + xtrad(c,"dom",r,d);
Backsolve xrowdem using E_xrowdem;

Variable
(all,c,COM)(all,r,ORG) xrowdem_d(c,r) # Eventually exported goods by origin #;
Equation E_xrowdem_d (all,c,COM)(all,r,ORG)
 ID01[ROWDEM_D(c,r)]*xrowdem_d(c,r) = sum{d,DST, ROWDEM(c,r,d)*xrowdem(c,r,d)};
Backsolve xrowdem_d using E_xrowdem_d;

! Excerpt 38 of TABLO input file: !
! Commodity contributions to selected national results !

Set COMMACROS # MAINMACROS with commodity components #
 (RealHou, RealInv, RealGov, ExpVol, ImpsLanded, CPI, ExportPI, ImpsLandedPI);
Subset COMMACROS is subset of MAINMACROS;
Variable (change)(all,c,COM)(all,m,COMMACROS)
   contComMacro(c,m) # Commodity contributions to national macro results #;

Equation
 E_contComMacroA
 (all,c,COM) WNATMACRO("RealHou")*contComMacro(c,"RealHou")
   = RATIOMMACRO("RealHou")*sum{d,DST, PUR_S(c,"Hou",d)*xhou_s(c,d)};
 E_contComMacroB
 (all,c,COM) WNATMACRO("RealInv")*contComMacro(c,"RealInv")
   = RATIOMMACRO("RealInv")*sum{d,DST, PUR_S(c,"Inv",d)*xinv_s(c,d)};
 E_contComMacroC
 (all,c,COM) WNATMACRO("RealGov")*contComMacro(c,"RealGov")
   = RATIOMMACRO("RealGov")*sum{d,DST, PUR_S(c,"Gov",d)*xgov_s(c,d)};
 E_contComMacroD
 (all,c,COM) WNATMACRO("ExpVol")*contComMacro(c,"ExpVol")
   = RATIOMMACRO("ExpVol")*sum{d,DST, PUR_S(c,"Exp",d)*xexp_s(c,d)};
 E_contComMacroE
 (all,c,COM) WNATMACRO("ImpsLanded")*contComMacro(c,"ImpsLanded")
   = RATIOMMACRO("ImpsLanded")*sum{r,ORG,TRADE_D(c,"imp",r)*xtrad_d(c,"imp",r)};
 E_contComMacroF
 (all,c,COM) WNATMACRO("CPI")*contComMacro(c,"CPI")
   = RATIOMMACRO("CPI")*sum{d,DST, PUR_S(c,"Hou",d)*ppur_s(c,"Hou",d)};
 E_contComMacroG
 (all,c,COM) WNATMACRO("ExportPI")*contComMacro(c,"ExportPI")
   = RATIOMMACRO("ExportPI")*sum{d,DST, PUR_S(c,"Exp",d)*ppur_s(c,"Exp",d)};
 E_contComMacroH
 (all,c,COM) WNATMACRO("ImpsLandedPI")*contComMacro(c,"ImpsLandedPI")
   = RATIOMMACRO("ImpsLandedPI")*sum{r,ORG, TRADE_D(c,"imp",r)*pimp(c,r)};

Variable ! equations to help check above !
 (change) (all,m,COMMACROS) NatComMacrox(m) # Sum of contComMacro(c,m) #;
          (all,m,COMMACROS) NatComMacro(m)  # Check:should = NatComMacrox(m) #;
Equation
E_NatComMacro  (all,m,COMMACROS) NatComMacro(m) = NatMacro(m);
E_NatComMacrox (all,m,COMMACROS) NatComMacrox(m) = sum{c,COM,contComMacro(c,m)};

! Excerpt 39 of TABLO input file: !
! Aggregation of regional to national industry output -- value added weights !

Coefficient (all,c,COM)(all,s,SRC) TRADE_RD(c,s)  # National direct demands #;
Formula     (all,c,COM)(all,s,SRC) TRADE_RD(c,s)  = sum{r,ORG, TRADE_D(c,s,r)};
Variable    (all,c,COM)(all,s,SRC) xtrad_rd(c,s)  # National direct demands #;
Equation E_xtrad_rd  (all,c,COM)(all,s,SRC) 
ID01[TRADE_RD(c,s)]*xtrad_rd(c,s) = sum{d,DST, TRADE_R(c,s,d)*xtrad_r(c,s,d)};

Variable
 (all,c,COM) natximp(c) # National imports #;
 (all,c,COM) natxexp(c) # National exports #;
Equation
 E_natximp (all,c,COM) natximp(c) = xtrad_rd(c,"imp");
 E_natxexp (all,c,COM) ID01[PUR_D(c,"dom","exp")]*natxexp(c) = 
       sum{d,DST,PUR(c,"dom","exp",d)*xexp(c,"dom",d)}; 

Coefficient  (all,c,COM) MAKE_ID(c) # National commodity outputs #;
Formula      (all,c,COM) MAKE_ID(c) = sum{d,DST, MAKE_I(c,d)};
Variable
 (all,c,COM) natxcom(c) # National commodity outputs #;
 (all,c,COM) natpdom(c) # National basic prices #;
Equation
 E_natxcom
  (all,c,COM) natxcom(c) = sum{r,REG, [MAKE_I(c,r)/MAKE_ID(c)]*xcom(c,r)};
 E_natpdom
  (all,c,COM) natpdom(c) = sum{r,REG, [MAKE_I(c,r)/MAKE_ID(c)]*pdom(c,r)};
 
! Excerpt 40 of TABLO input file: !
! Industry-length national aggregates !
Coefficient (all,i,IND) NATVTOT(i)   # Total national industry cost plus tax #;
            (all,i,IND) LAB_OD(i)    # Total national wage cost #;
            (all,i,IND) CAP_D(i)     # Total national capital rentals #;
            (all,i,IND) PRIM_D(i)    # Total value-added #;
Formula     (all,i,IND) NATVTOT(i)   = sum{d,DST,VTOT(i,d)};
            (all,i,IND) LAB_OD(i)    = sum{d,DST,LAB_O(i,d)};
            (all,i,IND) CAP_D(i)     = sum{d,DST,CAP(i,d)};
            (all,i,IND) PRIM_D(i)    = sum{d,DST,PRIM(i,d)};
Variable    (all,i,IND) natptot(i)   # National output price average #;
            (all,i,IND) natemploy(i) # National employment average #;
            (all,i,IND) natplab_o(i) # National wage average #;
            (all,i,IND) natwprim(i)  # National nominal factor payments #;
            (all,i,IND) natxcap(i)   # National capital use #;
            (all,i,IND) natpcap(i)   # National capital unit rental #;
            (all,i,IND) natxtot(i)   # National industry output #;
Equation
E_natptot (all,i,IND)
  ID01[NATVTOT(i)]*natptot(i)  = sum{d,DST,VTOT(i,d)*ptot(i,d)};
E_natemploy (all,i,IND)
  ID01[LAB_OD(i)]*natemploy(i) = sum{d,DST,LAB_O(i,d)*xlab_o(i,d)};
E_natplab_o (all,i,IND)
  ID01[LAB_OD(i)]*natplab_o(i) = sum{d,DST,LAB_O(i,d)*plab_o(i,d)};
E_natwprim (all,i,IND)
  ID01[PRIM_D(i)]*natwprim(i)  = sum{d,DST,PRIM(i,d)*wprim(i,d)};
E_natxcap (all,i,IND)
  ID01[CAP_D(i)]*natxcap(i)    = sum{d,DST,CAP(i,d)*xcap(i,d)};
E_natpcap (all,i,IND)
  ID01[CAP_D(i)]*natpcap(i)    = sum{d,DST,CAP(i,d)*pcap(i,d)};
E_natxtot (all,i,IND)
   NATVTOT(i)*natxtot(i)       = sum{d,DST,VTOT(i,d)*xtot(i,d)};
Backsolve natptot   using E_natptot;
Backsolve natemploy using E_natemploy;
Backsolve natwprim  using E_natwprim;
Backsolve natxcap   using E_natxcap;
! Compute regional contributions to national industry output !
Coefficient (all,i,IND) RATIOVTOT(i) # (Current/initial) ratio #;
Formula (initial)(all,i,IND) RATIOVTOT(i) =  1;
Variable (change)(all,i,IND)(all,d,DST)
   contnatxtot(i,d) # Regional contributions to national industry output #;
Update (all,i,IND) RATIOVTOT(i) =  natxtot(i);
Equation E_contnatxtot (all,i,IND)(all,d,DST)
   NATVTOT(i) *contnatxtot(i,d) = RATIOVTOT(i)*VTOT(i,d)*xtot(i,d);

! Excerpt 41 of TABLO input file: !
! Contributions of endowments, tech change and tax to national real GDP !
! Keller decomposition 
These are matrices (best viewed by ViewHAR from the SOL file) which add
up to the percent change in real GDP 
!

Set
 TECH # Tech changes # (alnd, acap, alab_o, aprim, atot, aint_s, atradmar); 
 TAXES # Tax contributions # (ProdTax, ComTax); 
 CONTINC # Contributors to real income gdp change # = FAC + TECH + TAXES;

Coefficient NATGDPINC # National income-side GDP #;
            RATIOXGDP # (Current/initial) ratio, national real gdp #;
Formula     NATGDPINC = sum{d,DST, GDPINC(d)};
  (initial) RATIOXGDP = 1;
Update      RATIOXGDP = NatMacro("RealGDP");

Variable
 (change)(all,i,IND)(all,d,DST)(all,c,CONTINC)
  contincind(i,d,c) # Industry contribution terms to national real income gdp #;
 (change)(all,i,IND)(all,c,CONTINC)
  contincind_d(i,c) # National industry contribution terms to real income gdp #;
Equation E_contincind
 (all,i,IND)(all,d,DST)(all,c,CONTINC) contincind(i,d,c) =
[RATIOXGDP/NATGDPINC]*{
  if[c="Lnd",     LND(i,d)*xlnd(i,d)]
+ if[c="Lab",     sum{o,OCC, LAB(i,o,d)*xlab(i,o,d)}]
+ if[c="Cap",     CAP(i,d)*xcap(i,d)]
- if[c="alnd",    LND(i,d)*alnd(i,d)]
- if[c="acap",    CAP(i,d)*acap(i,d)]
- if[c="alab_o",  LAB_O(i,d)*alab_o(i,d)]
- if[c="aprim",   PRIM(i,d)*aprim(i,d)]
- if[c="atot",    VCST(i,d)*atot(i,d)]
- if[c="aint_s",  sum{k,COM, PUR_S(k,i,d)*aint_s(k,i,d)}]
+ if[c="prodtax", PRODTAX(i,d)*xtot(i,d)]};
Equation E_contincind_d
 (all,i,IND)(all,c, CONTINC) contincind_d(i,c) =sum{d,DST, contincind(i,d,c)};

Variable
(change) (all,c,COM)(all,s,SRC)(all,d,DST)
   continccom(c,s,d) # COMTAX contribution terms to national real income gdp #;
Equation E_continccom
(all,c,COM)(all,s,SRC)(all,d,DST)  continccom(c,s,d) =
[RATIOXGDP/NATGDPINC]*{
  sum{i,IND, TAX(c,s,i,d)*xint(c,s,i,d)}
           + TAX(c,s,"hou",d)*xhou(c,s,d)
           + TAX(c,s,"inv",d)*xinv(c,s,d)
           + TAX(c,s,"gov",d)*xgov(c,s,d)
           + TAX(c,s,"exp",d)*xexp(c,s,d)};

Variable (change) (all,d,DST)(all,c,CONTINC) contincagg(d,c)
    # Combined contribution terms to national real income gdp #;
Equation E_contincagg
 (all,d,DST)(all,c,CONTINC) contincagg(d,c) = sum{i,IND, contincind(i,d,c)}
+ if[c="comtax", sum{cc,COM,sum{s,SRC, continccom(cc,s,d)}}]
- if[c="atradmar",[RATIOXGDP/NATGDPINC]*sum{k,COM,sum{s,SRC,sum{m,MAR,sum{r,ORG,
        TRADMAR(k,s,m,r,d)*atradmar(k,s,m,r,d)}}}}];

Variable (change) (all,c,CONTINC) contincagg_d(c)
    # Contribution terms to national real income gdp #;
Equation E_contincagg_d
 (all,c,CONTINC) contincagg_d(c) = sum{d,DST, contincagg(d,c)};

! make contincgdp, a less detailed version of contincagg_d !
Set TECHTAX  (Tech,Tax);  
 CONTINCA # Contributors to real income gdp change # = FAC + TECHTAX;
Variable (change) (all,c,CONTINCA) contincgdp(c)
    # Contribution terms to national real income gdp #;
Equation
E_contincgdpA (all,f,FAC) contincgdp(f) = contincagg_d(f);
E_contincgdpB contincgdp("Tech") = sum{t,TECH,  contincagg_d(t)};
E_contincgdpC contincgdp("Tax")  = sum{t,TAXES, contincagg_d(t)};

! Compute contributions to national real GDP at factor cost !
Coefficient NATGDPFAC # National real GDP at factor cost #;
            RATXGDPFAC # (Current/initial) ratio, XGDPFAC #;
Formula     NATGDPFAC = sum{d,DST, PRIM_I(d)};
  (initial) RATXGDPFAC = 1;
Update      RATXGDPFAC = xprim_id;
Set CONTINCB = FAC + "Tech";  
Subset CONTINCB is subset of CONTINCA;

Variable (change) (all,f,CONTINCB) contfacgdp(f)
    # Contribution terms to national real GDP at factor cost #;
Equation E_contfacgdp (all,f,CONTINCB)
 contfacgdp(f) = [RATXGDPFAC/NATGDPFAC]*[NATGDPINC/RATIOXGDP]*contincgdp(f);
                                        ! undoing previous !  

! COM and REG contributions to national export and import price indices !
Set TRADDIR (EXP, imp);
Coefficient (all,t,TRADDIR) RatioTOT(t) # Terms of Trade #;
Formula (initial)(all,t,TRADDIR) RatioTOT(t) = 1;
Update
 RatioTOT("exp") = NatMacro("ExportPI");
 RatioTOT("imp") = NatMacro("ImpsLandedPI");
Variable (change)(all,c,COM)(all,q,REG)(all,t,TRADDIR) contTrmsTrad(c,q,t)
   # Contributions to national export and import price indices #;

Equation E_contTrmsTradA
(all,c,COM)(all,q,REG) WNATMACRO("ExpVol")*contTrmsTrad(c,q,"exp") =
 RatioTOT("Exp")*PUR_S(c,"exp",q)*pfin("Exp",q);

Equation E_contTrmsTradB
(all,c,COM)(all,q,REG) WNATMACRO("ImpVolUsed")*contTrmsTrad(c,q,"imp") =
 RatioTOT("Imp")*TRADE_D(c,"imp",q)*pimp(c,q);

! Excerpt 42 of TABLO input file: !
! Variables to avoid reporting % changes in nothing !
! Below variables MUST be endogenous and should be backsolved !

Coefficient (parameter) SIGNIF # Threshold reporting value #;
Formula     (initial)   SIGNIF = 1;

Variable (all,c,COM)(all,d,DST) xexpSHO(c,d)
   # Export demands for domestic all-region composite leaving port at d #;
Equation E_xexpSHO (all,c,COM)(all,d,DST)
  xexpSHO(c,d) = if(USE(c,"dom","exp",d)>SIGNIF, xexp(c,"dom",d));

Variable (all,i,IND)(all,d,DST) xcapSHO(i,d) # Capital usage #;
Equation E_xcapSHO (all,i,IND)(all,d,DST)
  xcapSHO(i,d) = if(CAP(i,d)>SIGNIF, xcap(i,d));

Variable (all,i,IND)(all,d,DST) pcapSHO(i,d) # Rental price of capital #;
Equation E_pcapSHO (all,i,IND)(all,d,DST)
  pcapSHO(i,d) = if(CAP(i,d)>SIGNIF, pcap(i,d));

Variable (all,i,IND)(all,d,DST) xlab_oSHO(i,d) # Effective labour input #;
Equation E_xlab_oSHO (all,i,IND)(all,d,DST)
  xlab_oSHO(i,d) = if(LAB_O(i,d)>SIGNIF, xlab_o(i,d));

Variable (all,i,IND)(all,d,DST) xinvitotSHO(i,d) # Investment by industry #;
Equation E_xinvitotSHO (all,i,IND)(all,d,DST)
  xinvitotSHO(i,d) = if(INVEST_C(i,d)>SIGNIF, xinvitot(i,d));

Backsolve xexpSHO using E_xexpSHO;
Backsolve xcapSHO using E_xcapSHO;
Backsolve pcapSHO using E_pcapSHO;
Backsolve xlab_oSHO using E_xlab_oSHO;
Backsolve xinvitotSHO using E_xinvitotSHO;

! Excerpt 43 of TABLO input file: !
! Variables to show exports/imports of domestic goods to/from other regions !

Variable
(all,c,COM)(all,r,REG) xdomexp(c,r)
  # Amount good c made in r sent to other domestic regions (non-margin) #;
(all,c,COM)(all,d,REG) xdomimp(c,d)
# Amount domestic good c used in d made in other domestic regions (non-margin)#;
(all,r,REG) xdomexp_c(r)
  # Amount goods made in r sent to other domestic regions (non-margin) #;
(all,d,REG) xdomimp_c(d)
# Amount domestic goods used in d made in other domestic regions (non-margin)#;
Coefficient
(all,c,COM)(all,r,REG) VDOMEXP(c,r)
 # Value good c made in r sent to other domestic regions (non-margin) #;
(all,c,COM)(all,d,REG) VDOMIMP(c,d)
 # Value domestic good c used in d made in other domestic regions (non-margin)#;
(all,r,REG) VDOMEXP_C(r)
 # Value goods made in r sent to other domestic regions (non-margin) #;
(all,d,REG) VDOMIMP_C(d)
 # Value domestic goods used in d made in other domestic regions (non-margin)#;
Formula
(all,c,COM)(all,r,REG) VDOMEXP(c,r) = TRADE_D(c,"dom",r) - TRADE(c,"dom",r,r);
(all,c,COM)(all,d,REG) VDOMIMP(c,d) = TRADE_R(c,"dom",d) - TRADE(c,"dom",d,d);
(all,r,REG) VDOMEXP_C(r) = sum{c,COM,VDOMEXP(c,r)};
(all,d,REG) VDOMIMP_C(d) = sum{c,COM,VDOMIMP(c,d)};
Write
 VDOMEXP to file SUMMARY header "VDEX";
 VDOMIMP to file SUMMARY header "VDIM";

Equation E_xdomexp
(all,c,COM)(all,r,REG) ID01[VDOMEXP(c,r)]*xdomexp(c,r) =
 TRADE_D(c,"dom",r)*xtrad_d(c,"dom",r) - TRADE(c,"dom",r,r)*xtrad(c,"dom",r,r);

Equation E_xdomimp
(all,c,COM)(all,d,REG) ID01[VDOMIMP(c,d)]*xdomimp(c,d) =
 TRADE_R(c,"dom",d)*xtrad_r(c,"dom",d) - TRADE(c,"dom",d,d)*xtrad(c,"dom",d,d);

Equation E_xdomexp_c (all,r,REG)
  ID01[VDOMEXP_C(r)]*xdomexp_c(r) = sum{c,COM,VDOMEXP(c,r)*xdomexp(c,r)};
Backsolve xdomexp_c using E_xdomexp_c;

Equation E_xdomimp_c (all,d,REG)
  ID01[VDOMIMP_C(d)]*xdomimp_c(d) = sum{c,COM,VDOMIMP(c,d)*xdomimp(c,d)};
Backsolve xdomimp_c using E_xdomimp_c;

Variable
(all,c,COM)(all,r,REG) xdomloc(c,r) # Amount good c made and used in r #;
Equation E_xdomloc
(all,c,COM)(all,r,REG) xdomloc(c,r) = xtrad(c,"dom",r,r);
Backsolve xdomloc using E_xdomloc;

! Excerpt 44 of TABLO input file: !
! Summary tables of main uses!

Coefficient
(all,c,COM)(all,s,SRC)(all,m,MAINUSR)(all,d,DST) VMAINUSE(c,s,m,d)
# Simplified USE matrix with all industries combined, delivered values #;
Formula
(all,c,COM)(all,s,SRC)(all,d,DST)
   VMAINUSE(c,s,"INT",d)= sum{i,IND, USE(c,s,i,d)};
(all,c,COM)(all,s,SRC)(all,m,FINDEM)(all,d,DST)
   VMAINUSE(c,s,m,d) = USE(c,s,m,d);
Write VMAINUSE to file SUMMARY header "VMNU";

! Local (and other) sales table for summary file !
Coefficient (all,c,COM)(all,s,SRC)(all,r,REG)
    LOCSHR(c,s,r) # Share local-made basic in direct use (delivered) #;
Formula     (all,c,COM)(all,s,SRC)(all,r,REG)
    LOCSHR(c,s,r) = TRADE(c,s,r,r)/ID01[USE_U(c,s,r)];
Write LOCSHR to file SUMMARY header "LCSR";

Coefficient (all,c,COM)(all,s,SRC)(all,r,REG)
    LOCSHR_R(c,s,r) # Share basic in delivered #;
Formula     (all,c,COM)(all,s,SRC)(all,r,REG)
    LOCSHR_R(c,s,r) = TRADE_R(c,s,r)/ID01[USE_U(c,s,r)];
Write LOCSHR_R to file SUMMARY header "LCRR";

Set  MAINDEST # Main destinations #  (INT, HOU, INV, GOV, MAR, OthReg, RowEXP);
Set  MAINDESTX # Local direct main destinations #  (INT, HOU, INV, GOV);
Subset  MAINDESTX is subset of MAINDEST;
Subset  MAINDESTX is subset of MAINUSR;

Coefficient
(all,c,COM)(all,r,REG)(all,d,MAINDEST) VMAINDEST(c,r,d)
# Main uses of dom com c made in r, basic values #;
Formula
(all,c,COM)(all,r,REG)(all,d,MAINDEST) VMAINDEST(c,r,d) = 42;
(all,c,COM)(all,r,REG)(all,d,MAINDESTX)
   VMAINDEST(c,r,d) = LOCSHR(c,"dom",r)*VMAINUSE(c,"dom",d,r);
(all,c,NONMAR)(all,r,REG)VMAINDEST(c,r,"MAR") = 0;
(all,m,MAR)(all,p,REG)VMAINDEST(m,p,"MAR") =
   sum{r,ORG, sum{d,DST, SUPPMAR(m,r,d,p)}};
(all,c,COM)(all,r,REG)VMAINDEST(c,r,"OthReg") =
   TRADE_D(c,"dom",r)-TRADE(c,"dom",r,r);
(all,c,COM)(all,r,REG)VMAINDEST(c,r,"RowEXP") =
   LOCSHR(c,"dom",r)*USE(c,"dom","Exp",r);
Write VMAINDEST to file SUMMARY header "VMDS";

Coefficient (all,c,COM)(all,d,DST) CHECKDEST(c,d) # Check -- should be tiny #;
Formula     (all,c,COM)(all,d,DST) CHECKDEST(c,d) = 42;
 (all,c,COM)(all,d,REG)
  CHECKDEST(c,d) = MAKE_I(c,d) -  sum{q,MAINDEST,VMAINDEST(c,d,q)};
Write CHECKDEST to file SUMMARY header "CDST";

Coefficient (all,c,COM)(all,d,MAINDEST)
   NATMAINDEST(c,d) # Main uses of dom com c nationally, basic values #;
Formula
(all,c,COM)(all,d,MAINDEST) NATMAINDEST(c,d) = 42; ! should be overwritten !
(all,c,COM)(all,d,MAINDESTX)
    NATMAINDEST(c,d) = sum{r,REG, LOCSHR_R(c,"dom",r)*VMAINUSE(c,"dom",d,r)};
(all,c,NONMAR) NATMAINDEST(c,"MAR") = 0;
(all,m,MAR) NATMAINDEST(m,"MAR") = sum{p,REG,VMAINDEST(m,p,"MAR")};
(all,c,COM) NATMAINDEST(c,"OthReg") = 0;
(all,c,COM) NATMAINDEST(c,"RowEXP") =
           sum{r,REG, LOCSHR_R(c,"dom",r)*VMAINUSE(c,"dom","EXP",r)};
Write NATMAINDEST to file SUMMARY header "NMDS";

Coefficient (all,c,COM)(all,d,MAINDEST) DIFFDEST(c,d)
   # Reconciliation of VMAINDEST with NATMAINDEST #;
Formula     (all,c,COM)(all,d,MAINDEST) DIFFDEST(c,d)
               = sum{r,REG,VMAINDEST(c,r,d)} - NATMAINDEST(c,d);
Write DIFFDEST to file SUMMARY header "DDST";

! Summary file diagnostic to check if C, I, G, etc follow regional GDP !
Coefficient (all,d,DST)(all,f,FINDEM) RATFINPUR(f,d);
Formula     (all,d,DST)(all,f,FINDEM) RATFINPUR(f,d) = PUR_CS(f,d);
Write RATFINPUR to file SUMMARY header "FPUR"
  longname "Final demands, purchasers values";
Formula     (all,d,DST)(all,f,FINDEM) RATFINPUR(f,d) = RATFINPUR(f,d)/PRIM_I(d);
Write RATFINPUR to file SUMMARY header "RPUR"
  longname "[final demands, purchasers value]/[Reg GDP]";

! Excerpt 45 of TABLO input file: !
! Trade Exposure summaries !

! Trade pressure = estimated effect on demand of 1% price rise, due to trade
  = import share * Armington elasticity
  + export share * Export demand elasticity !
Set TRDSUM 
(OutPut, Exports, Imports, ShrExport, ShrImport, ExpEls, ImpSub, Pressure); 
Coefficient (all,c,COM)(all,t,TRDSUM) TRADESUM(c,t)
# Summary Table of Trade Exposure (national) #;
Formula (all,c,COM)(all,t,TRDSUM) TRADESUM(c,t) =0;
(all,c,COM) TRADESUM(c,"OutPut") =  MAKE_ID(c);
(all,c,COM) TRADESUM(c,"Exports") = NATMAINDEST(c,"RowEXP");
(all,c,COM) TRADESUM(c,"Imports") = sum{d,DST, TRADE_R(c,"imp",d)};
(all,c,COM) TRADESUM(c,"ShrExport") =TRADESUM(c,"Exports")/TRADESUM(c,"OutPut");
(all,c,COM) TRADESUM(c,"ShrImport") = TRADESUM(c,"Imports")/
  [TRADESUM(c,"OutPut")+TRADESUM(c,"Imports")-TRADESUM(c,"Exports")];
(all,c,COM) TRADESUM(c,"ExpEls") =EXP_ELAST(c);
(all,c,COM) TRADESUM(c,"ImpSub") =SIGMADOMIMP(c);
(all,c,COM) TRADESUM(c,"Pressure") = TRADESUM(c,"ShrExport")*EXP_ELAST(c)
                                   + TRADESUM(c,"ShrImport")*SIGMADOMIMP(c);
Write TRADESUM to file SUMMARY header "TSUM";

Coefficient (all,d,DST) TRDPRESSURE(d) # Regional Measure of Trade Exposure #;
Formula     (all,d,DST) TRDPRESSURE(d) = 
  sum{c,COM,TRADESUM(c,"Pressure")*MAKE_I(c,d)}/sum{c,COM, MAKE_I(c,d)};
Write TRDPRESSURE to file SUMMARY header "TPRG";

Set ELSTRAD # Trade elasticities # (SIGMADOMIMP,SIGMADOMDOM,EXP_ELAST);
Coefficient (all,c,COM)(all,e,ELSTRAD) TRADELAST(c,e) # Trade elasticities #;
Formula (all,c,COM)(all,e,ELSTRAD) TRADELAST(c,e) = 0;
 (all,c,COM) TRADELAST(c,"EXP_ELAST")   = EXP_ELAST(c);
 (all,c,COM) TRADELAST(c,"SIGMADOMIMP") = SIGMADOMIMP(c);
 (all,c,COM) TRADELAST(c,"SIGMADOMDOM") = SIGMADOMDOM(c);
Write TRADELAST to file SUMMARY header "TREL";

![[!
! Excerpt 46 of TABLO input file: !
! Decomposition of sales change by destination !

Coefficient
 (all,c,COM) INITSALES(c) # Initial volume of SALES at current prices #;
Formula
 (initial) (all,c,COM) INITSALES(c) = SALES(c);
Update (all,c,COM) INITSALES(c) = p0com(c);
Set DESTPLUS # Sale Categories #
 (Interm, Invest, HouseH, Export, GovGE, Stocks, Margins, Total);
Subset DEST is subset of DESTPLUS;
Variable (change)
  (all,c,COM)(all,d,DESTPLUS) SalesDecomp(c,d) # Sales decomposition #;
Equation
 E_SalesDecompA
(all,c,COM)(all,d,DEST) INITSALES(c)*SalesDecomp(c,d) = 100*delSale(c,"dom",d);
 E_SalesDecompB
 (all,c,COM) SalesDecomp(c,"Total")= sum{d,DEST, SalesDecomp(c,d)};!]]!

! Excerpt 47 of TABLO input file: !
! Variables for BOTE explanations !

! Effective price of labour -- including labour-saving tech change !
Variable pLabEff # Effective price of labour, inc. labour-saving tech change #;
Equation E_pLabEff
 sum{i,IND, sum{d,DST, LAB_O(i,d)*[plab_o(i,d)+alab_o(i,d)-pLabEff]}}=0;

Set BOTEVARS # useful price ratios for BOTE # (pLabEf_pprim, pLabEf_CPI,
CPI_pGNE, pGNE_pGDP, pGDP_pprim, pcap_pprim, pcap_pinv, pinv_pgne);
Variable (all,b,BOTEVARS) bote(b) # useful price ratios for BOTE #;  
Equation
E_boteA Bote("pLabEf_pprim")= pLabEff - pprim_id;
E_boteB Bote("pLabEf_CPI")  = pLabEff - natpfin("Hou");! constant in short run?!
E_boteC Bote("CPI_pGNE")    = natpfin("Hou") - natpgne;
E_boteD Bote("pGNE_pGDP")   = natpgne- natpgdpexp;       ! = f(terms of trade) !
E_boteE Bote("pGDP_pprim")  = natpgdpexp - pprim_id;     ! = f(indirect taxes) !
E_boteF Bote("pcap_pprim")  = pcap_id - pprim_id;
E_boteG Bote("pcap_pinv")   = pcap_id - natpfin("Inv"); ! constant in long run?!
E_boteH Bote("pinv_pgne")   = natpfin("Inv") - natpgne;! might not change much !

Coefficient TRDSHR # trade share in GDP (approx, used in BOTE) #;
Formula   TRDSHR = 0.5*[NATGDPEXPSUM("EXP")-NATGDPEXPSUM("Imports")]/NATGDPEXP;
Write     TRDSHR to file SUMMARY header "TSHR";

! Excerpt 48 of TABLO input file: !
! Estimate of Basic value USE !

Coefficient
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST) BASIC(c,s,u,d) # Basic USE values#;
(all,c,COM)(all,u,USR)(all,d,DST) DOMBASIC(c,u,d) # Basic USE values#;
(all,c,COM)(all,d,DST) DOMSALES(c,d) # All-user Basic USE values#;
(all,c,COM)(all,d,DST) EXPSALES(c,d) # Export Basic USE values#;
(all,c,COM) NATDOMSALES(c) # National All-user Basic USE values#;
(all,c,COM) NATLOCSALES(c) # National All-nonexportuser Basic USE values#;
Formula
(all,c,COM)(all,s,SRC)(all,u,USR)(all,d,DST)
   BASIC(c,s,u,d) = USE(c,s,u,d)*TRADE_R(c,s,d)/ID01[DELIVRD_R(c,s,d)]; 
(all,c,COM)(all,u,USR)(all,d,DST) DOMBASIC(c,u,d) = BASIC(c,"dom",u,d);
(all,c,COM)(all,d,DST) DOMSALES(c,d) = sum{u, USR, DOMBASIC(c,u,d)};
(all,c,COM)(all,d,DST) EXPSALES(c,d) = DOMBASIC(c,"exp",d); 
(all,c,COM) NATDOMSALES(c) = sum{d,DST, DOMSALES(c,d)};
(all,c,COM) NATLOCSALES(c) = NATDOMSALES(c) - sum{d,DST, EXPSALES(c,d)};

Write BASIC to file SUMMARY header "BAS";

! Excerpt 49 of TABLO input file: !
! Decomposition of Fan !

Coefficient (all,c,COM) INITSALES(c) # INITIAL sales at CURRENT prices #;
Formula (initial) (all,c,COM) INITSALES(c) = NATDOMSALES(c);
Update (all,c,COM) INITSALES(c) = natpdom(c);
 
Set FANCAT # Parts of Fan decomposition# (LocalMarket, DomShare, Export, Total);
Variable
(change)(all,c,NONMAR)(all,f,FANCAT) fandecomp(c,f) # Fan decomposition #;
Equation
! The local market effect is the % change in output that would have occurred
if local sales of the domestic product had followed dom+imp sales (xlocuse_sd) ! 
 E_fandecompA # Growth in local market effect #  (all,c,NONMAR)
  INITSALES(c)*fandecomp(c,"LocalMarket") = NATLOCSALES(c)*xlocuse_sd(c);

 E_fandecompB # Export effect # 
  (all,c,NONMAR) INITSALES(c)*fandecomp(c,"Export") =
       sum{d,DST, EXPSALES(c,d)*xexp(c,"dom",d)};

 E_fandecompC # Import substitution effect - via residual #
  (all,c,NONMAR) fandecomp(c,"Total") = fandecomp(c,"LocalMarket")
     + fandecomp(c,"DomShare") + fandecomp(c,"Export");

 E_fandecompD # Fan total = natxcom #
  (all,c,NONMAR) INITSALES(c)*fandecomp(c,"Total") = NATDOMSALES(c)*natxcom(c);

! Notes: (a) natxcom does not include inventory changes;  
(b) "DomShare" or Import substitution effect includes not only effect of 
  dom/imp relative price change, but also effect of shifting between uses with
  different imp/dom ratios !

! Excerpt 50 of TABLO input file: !
! Investment ratio summary -- identify outlying INV/RENTAL ratios !

Set NATREG (National);
Set TOTIND (TotalInds);
Set REGPLUS = REG+NATREG;
Set INDPLUS = IND+TOTIND;
Set INVFACT (INVEST,CapRental,Output,InvOverCap,InvOverOut);
Coefficient
 (all,r,REGPLUS)(all,i,INDPLUS)(all,z,INVFACT)
       INVFACTS(r,i,z) # Investment ratio summary #;
 (all,i,IND)(all,z,INVFACT) INVFACTS_r(i,z) # Temp total #;
 (all,r,REGPLUS)(all,z,INVFACT) INVFACTS_I(r,z) # Temp total #;
Formula (all,r,REGPLUS)(all,i,INDPLUS)(all,z,INVFACT) INVFACTS(r,i,z)=0;
 (all,r,REG)(all,i,IND) INVFACTS(r,i,"Invest")    = INVEST_C(i,r);
 (all,r,REG)(all,i,IND) INVFACTS(r,i,"CapRental") = CAP(i,r);
 (all,r,REG)(all,i,IND) INVFACTS(r,i,"Output")    = VTOT(i,r);
 (all,i,IND)(all,z,INVFACT) INVFACTS_r(i,z) = sum{r,REG, INVFACTS(r,i,z)};
 (all,i,IND)(all,z,INVFACT) INVFACTS("National",i,z) = INVFACTS_r(i,z);
 (all,r,REGPLUS)(all,z,INVFACT) INVFACTS_I(r,z) = sum{i,IND, INVFACTS(r,i,z)};
 (all,r,REGPLUS)(all,z,INVFACT) INVFACTS(r,"TotalInds",z) = INVFACTS_I(r,z);
(all,r,REGPLUS)(all,i,INDPLUS) INVFACTS(r,i,"InvOverCap") =
               INVFACTS(r,i,"Invest")/ID01[INVFACTS(r,i,"CapRental")];
(all,r,REGPLUS)(all,i,INDPLUS) INVFACTS(r,i,"InvOverOut") =
               INVFACTS(r,i,"Invest")/ID01[INVFACTS(r,i,"Output")];
 Write INVFACTS to file SUMMARY header "INVF";

! Excerpt 51 of TABLO input file: !
! Weights for variable aggregation of results!
Coefficient
 (all,c,COM)(all,q,REG) Import(c,q);
 (all,c,COM)(all,q,REG) Export(c,q);
Formula
 (all,c,COM)(all,q,REG) Import(c,q) = TRADE_D(c,"imp",q);
 (all,c,COM)(all,q,REG) Export(c,q) = PUR_S(c,"exp",q);
Write
Import   to file SUMMARY header "IMP"  longname "Regional imports from ROW";
HOUSHR   to file SUMMARY header "HOUP";
INVEST_I to file SUMMARY header "VINV" longname "Investment costs";
LAB_O    to file SUMMARY header "LAB"  longname "Labour costs";
PRIM     to file SUMMARY header "PRIM" longname "Primary factor payments";
VARCST   to file SUMMARY header "VCST" longname "Variable costs";
CAP      to file SUMMARY header "CAP"  longname "Capital rentals";
Export   to file SUMMARY header "VEXP" longname "Exports";
NATVTOT  to file SUMMARY header "NATV" longname "National output";
HOUPUR_H to file SUMMARY header "HOUS";
Set WAGG
(xtot@@VTOT,
ptot@@VTOT,
xcap@@CAP,
pcap@@CAP,
pimp@@IMP,
xhou@@HOUP,
xinv@@VINV,
plab@@LAB,
xlab@@LAB,
aprm@@PRIM,
pvar@@VCST,
xexp@@VEXP,
ximp@@IMP,
nvto@@@@NATV,
munx@@@@MVTO,
xhos@@@@HOUS);
Write (Set) WAGG to file SUMMARY header "WAGG";

! Excerpt 52 of TABLO input file: !
! Check of accounting identities !

Zerodivide default 1;

Coefficient
 EP # Tiny value #;
 (all,i,IND)(all,d,DST) CHECKA(i,d) # Net Output - MAKE_C #;
 (all,i,IND)(all,d,DST) CKRATA(i,d) # Net Output / MAKE_C #;
Formula
EP=0.00001;
(all,i,IND)(all,d,DST) CHECKA(i,d) = [VTOT(i,d) - STOCKS(i,d)] - MAKE_C(i,d);
(all,i,IND)(all,d,DST) CKRATA(i,d) =
                        [EP+VTOT(i,d) - STOCKS(i,d)] / [EP+MAKE_C(i,d)];
Write
 MAKE_C to file SUMMARY header "MAKC";
 STOCKS to file SUMMARY header "STOK";
 CHECKA to file SUMMARY header "CHKA";
 CKRATA to file SUMMARY header "CKRA";

Coefficient
(all,c,COM)(all,s,SRC)(all,d,DST) CHECKB(c,s,d) # USE_U - DELIVRD_R #;
(all,c,COM)(all,s,SRC)(all,d,DST) CKRATB(c,s,d) # USE_U / DELIVRD_R #;
Formula
(all,c,COM)(all,s,SRC)(all,d,DST) CHECKB(c,s,d) = USE_U(c,s,d)-DELIVRD_R(c,s,d);
(all,c,COM)(all,s,SRC)(all,d,DST) CKRATB(c,s,d) =
                            [EP+USE_U(c,s,d)]/[EP+DELIVRD_R(c,s,d)];
Write
 USE_U  to file SUMMARY header "USEU";
 DELIVRD_R  to file SUMMARY header "DVDR";
 CHECKB to file SUMMARY header "CHKB";
 CKRATB to file SUMMARY header "CKRB";

Coefficient
 (all,c,COM)(all,r,REG) CHECKC(c,r) # MAKE_I - demands #;
 (all,c,COM)(all,r,REG) CKRATC(c,r) # MAKE_I / demands #;
 (all,c,COM)(all,r,REG) DEMANDS(c,r) # Demands #;
Formula
 (all,c,COM)(all,r,REG) DEMANDS(c,r) = TRADE_D(c,"dom",r);
 (all,m,MAR)(all,r,REG) DEMANDS(m,r) = DEMANDS(m,r) + SUPPMAR_RD(m,r);
 (all,c,COM)(all,r,REG) CHECKC(c,r)  = MAKE_I(c,r) - DEMANDS(c,r);
 (all,c,COM)(all,r,REG) CKRATC(c,r)  = [EP+MAKE_I(c,r)] / [EP+DEMANDS(c,r)];
Write
 CHECKC  to file SUMMARY header "CHKC";
 CKRATC  to file SUMMARY header "CKRC";
 MAKE_I  to file SUMMARY header "MAKI";
 DEMANDS to file SUMMARY header "DMDS";

Coefficient
(all,m,MAR)(all,r,ORG)(all,d,DST) CHECKD(m,r,d) # TRADMAR_CS - SUPPMAR_P #;
(all,m,MAR)(all,r,ORG)(all,d,DST) CKRATD(m,r,d) # TRADMAR_CS / SUPPMAR_P #;
Formula
 (all,m,MAR)(all,r,ORG)(all,d,DST)
   CHECKD(m,r,d) = TRADMAR_CS(m,r,d) - SUPPMAR_P(m,r,d);
 (all,m,MAR)(all,r,ORG)(all,d,DST)
   CKRATD(m,r,d) = [EP+TRADMAR_CS(m,r,d) ]/ [EP+SUPPMAR_P(m,r,d)];
Write
 TRADMAR_CS to file SUMMARY header "TMCS";
 SUPPMAR_P  to file SUMMARY header "SPMP";
 CHECKD     to file SUMMARY header "CHKD";
 CKRATD     to file SUMMARY header "CKRD";

Coefficient
(all,c,COM)(all,d,DST) CHECKE(c,d) # INVEST_I - PUR_S(inv) #;
(all,c,COM)(all,d,DST) CKRATE(c,d) # INVEST_I / PUR_S(inv) #;
Formula (all,c,COM)(all,d,DST) CHECKE(c,d) = INVEST_I(c,d) - PUR_S(c,"Inv",d);
 (all,c,COM)(all,d,DST) CKRATE(c,d)   =
                      [EP+INVEST_I(c,d)] / [EP+PUR_S(c,"Inv",d)];
Write
 INVEST_I to file SUMMARY header "INVI";
 PUR_S    to file SUMMARY header "PURS";
 CHECKE   to file SUMMARY header "CHKE";
 CKRATE   to file SUMMARY header "CKRE";
Zerodivide off;

Set CONSTRAINTS (A,B,c,d,e);
Coefficient (all,c,CONSTRAINTS) SUMABS(c) # Sum absolute errors #;
Formula
 SUMABS("A") = sum{i,IND, sum{d,DST,            ABS[CHECKA(i,d)]}};
 SUMABS("B") = sum{c,COM, sum{s,SRC, sum{d,DST, ABS[CHECKB(c,s,d)]}}};
 SUMABS("C") = sum{c,COM, sum{r,REG,            ABS[CHECKC(c,r)]}};
 SUMABS("D") = sum{m,MAR, sum{r,ORG, sum{d,DST, ABS[CHECKD(m,r,d)]}}};
 SUMABS("E") = sum{c,COM, sum{d,DST,            ABS[CHECKE(c,d)]}};
Write SUMABS to file SUMMARY header "SMAB";

Coefficient TOL3;
Formula     TOL3 = 0.01;
Assertion
# RATA near 1 # (all,i,IND)(all,d,DST)            ABS[1-CKRATA(i,d)]   < TOL3;
# RATB near 1 # (all,c,COM)(all,s,SRC)(all,d,DST) ABS[1-CKRATB(c,s,d)] < TOL3;
# RATC near 1 # (all,c,COM)(all,r,REG)            ABS[1-CKRATC(c,r)]   < TOL3;
# RATD near 1 # (all,m,MAR)(all,r,ORG)(all,d,DST) ABS[1-CKRATD(m,r,d)] < TOL3;
# RATE near 1 # (all,c,COM)(all,d,DST)            ABS[1-CKRATE(c,d)]   < TOL3;
! alternate form, which does not check ratio values where diff is tiny 
# RATA near 1 # (all,i,IND)(all,d,DST:ABS[CHECKA(i,d)]>0.1)
            ABS[1-CKRATA(i,d)]   < TOL3;
# RATB near 1 # (all,c,COM)(all,s,SRC)(all,d,DST:ABS[CHECKB(c,s,d)]>0.1)
            ABS[1-CKRATB(c,s,d)] < TOL3;
# RATC near 1 # (all,c,COM)(all,r,REG:ABS[CHECKC(c,r)]>0.1)
            ABS[1-CKRATC(c,r)]   < TOL3;
# RATD near 1 # (all,m,MAR)(all,r,ORG)(all,d,DST:ABS[CHECKD(m,r,d)]>0.1)
            ABS[1-CKRATD(m,r,d)] < TOL3;
# RATE near 1 # (all,c,COM)(all,d,DST:ABS[CHECKE(c,d)]>0.1)
            ABS[1-CKRATE(c,d)]   < TOL3; !

! Excerpt 53 of TABLO input file: !
! Condensation Instructions !

Omit fgov flab ahou_s asuppmar bint_s;
Backsolve realwage using E_realwage;
Backsolve pvar using E_pvar;
Backsolve contincind_d using E_contincind_d;
Backsolve delPRIM using E_delPRIM;
Backsolve pmake using E_pmake;
Backsolve aint_s using E_aint_s;
Backsolve xSUPPMAR_d using E_xSUPPMAR_d;
Backsolve delTAXhou using E_delTAXhou;
Backsolve ppur_s using E_ppur_s;
Backsolve delTAXint using E_delTAXint;
Backsolve xlab using E_xlab;
Backsolve xSUPPMAR using E_xSUPPMAR;
Backsolve xexp using E_xexp;
Backsolve xtrad_d using E_xtrad_d;
Backsolve delTAXinv using E_delTAXinv;
Backsolve xprim using E_xprim;
Backsolve xsub using E_xsub;
Backsolve xTRADMAR using E_xTRADMAR;
Backsolve contxgdpexp using E_contxgdpexp;
Backsolve pdelivrd using E_pdelivrd;
Backsolve xhouh_s using E_xhouh_s;
Backsolve xhou_s using E_xhou_s;
Backsolve xstocks using E_xstocks;
Backsolve xint_s using E_xint_s;
Backsolve xinvi using E_xinvi;
Backsolve plab using E_plab;
Backsolve xlab_o using E_xlab_o;
Backsolve plab_o using E_plab_o;
Backsolve xmake using E_xmake;
Backsolve pcst using E_pcst;
Backsolve delTAXexp using E_delTAXexp;
Backsolve xlux using E_xlux;
Backsolve xTRAD using E_xTRAD;
Backsolve contxprim_i using E_contxprim_i;
Backsolve pprim using E_pprim;
Backsolve delPTX using E_delPTX;
Backsolve pfin using E_pfin;
Backsolve delTAXgov using E_delTAXgov;
Backsolve xint using E_xint;
Backsolve pINVEST using E_pINVEST;
Backsolve xinv using E_xinv;
Backsolve xlab_i using E_xlab_i;
Backsolve pSUPPMAR_p using E_pSUPPMAR_p;
Backsolve xGov using E_xgov;
Backsolve contnatxtot using E_contnatxtot;
Backsolve xinv_s using E_xinv_s;
Backsolve wlab_i using E_wlab_i;
Backsolve ppur using E_ppur;
Backsolve contMainMacro using E_contMainMacro;
Backsolve contCPI using E_contCPI;
Backsolve continccom using E_continccom;

! end !
 


